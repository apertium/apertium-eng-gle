# ==================================================================== #
# I R I S H   D I S A M B I G U A T I O N  
# ==================================================================== #
# Elaine Uí Dhonnchadha
# 2004-2015
# Christoph Wendler 
# 2008-2009
# Gearóid Ó Donnchadha 
# 2015
# ==================================================================== #
# "ar" => LEMMA, "<ar>" => WORDFORM
#
#FIND ====>>>>
# distinguish between nach cop/verbpart by using +Ecl +Len
# i.e copula doesnt cause initial mutations - verb parts do ...
#
# Delimiters
# Sets
# Disambiguation rules
# ==================================================================== #
# SENTENCE DELIMITERS
# ==================================================================== #

DELIMITERS = "<.>" "<!>" "<?>" "<#>" "<</p>>" "<</s>>" "<...>" ;

PREFERRED-TARGETS = prn n pii prs ;

# ==================================================================== #
# SETS
# ==================================================================== #
SETS 
LIST BOS = (>>>) "<p>" "<s>" (ChildesID);
LIST EOS = (<<<) (sent); # end and beg. of sentence. for vislcg.

LIST COMMA = "<,>" ;
# Todo
LIST PUNCT-INT = (Punct Int) (Punct Bar) (lpar) (rpar);

LIST CLB = (rel) (cnjcoo) (cnjsub) (cmpl) ;
SETS

LIST COP-COND = "<Ba>" "<Ar>" "<B'>" "<Nár>" "<nár>" "<Arbh>";

# attributive adj. set
LIST ADJ-ATTR = (adj nom) (adj gen) (adj voc) ;

LIST ADJ-NOT-VA = (adj nom) (adj gen) (adj voc) (Adj Base) ;

# adjectives follow nouns but the following is 
# a list of the few adjectives can precede a noun
LIST ADJ-PRENOM = "droch" "sean" "príomh" "fíor" "iontach" "dearg" "leath" "corr" "gnáth" "mór"; 

# Any noun other than verbal-noun
# the are several types of Noun: +Noun, Subst+Noun, Prop+Noun, Verbal+Noun,
# Guess+Noun, but all nouns except verbal nouns have number (even guess nouns)
LIST NOUN-NOT-VN = (n m sg) (n m pl) (n f sg) (n f pl) ;

# a list of items which can precede a noun
LIST NOUN-PREMOD = (det def) (det pos) (det qnt) (num) ADJ-PRENOM ;

# a list of items which can follow a simple preposition
# (art def is used to exclude "sa" e.g. "shuigh sé faoi sa chathaoir"
# rel clause: an rud as ar/vpart Rel(not Cop) tháinig
# thar/Prep a/Prep bheith/VNoun
# mar iad/Pron Pers
LIST POST-PREP = (n) (det def) (det) (prn) (num) (part nm) ADJ-PRENOM (vpart rel) (pr) (quot);

# "a" functions as a simple prep in following phrases
LIST A-PREP-PHR = "chlog" "chois" "chóir" "dhíth" ; 

# the genitive follows some simple prepositions and partitives, as well as another noun, verbal noun or compound preposition
LIST GEN-SIMP-PREP = "chun" "trasna" "timpeall" "fearacht" "dála" "cois" ;

#LIST GEN-PART = "roinnt" "cuid" "morán" "lán" "méid" "dosaen" "péire" "scór" ;
# Extended list - CW 09/08
# included mó & lú
LIST GEN-PART = "beagáinín" "beagán" "cineál" "cuid" "deamhan" "dosaen" "iomarca" "lán" "méid" "mórán" "péire" "puinn" "roinnt" "saghas" "scór" "sórt" "<mó>" "<lú>";

# lemmas include emphatic forms
LIST OBJ-PRON = "í" "é" "iad" ;

# wordform rather than lemma is used as we do not want to include thrí or dtrí etc.
LIST NUM-COUNT = "<haon>" "<dó>" "<trí>" "<ceathair>" "<cúig>" "<sé>" "<seacht>" "<hocht>" "<naoi>" "<deich>" "<hAon>" "<Dó>" "<Trí>" "<Ceathair>" "<Cúig>" "<Sé>" "<Seacht>" "<hOcht>" "<Naoi>" "<Deich>";
LIST NUM-LEN = "aon" "<chéad>" "<dhá>" "trí" "<ceithre>" "cúig" "sé" "beirt" "uile";
LIST NUM-ECL = "seacht" "ocht" "naoi" "deich" ;
LIST NUM-PL-ADJ = "<dhá>" "trí" "ceithre" "cúig" "sé" "seacht" "ocht" "naoi" "deich" "beirt" ;
# prepositions commonly used before verbal nouns
# "ar" => lemma, "<ar>" => wordform
LIST PREP-VN = "<ag>" "<le>" "<gan>" (part inf) "<á>" ("<ar>" Prep) "tar éis" "chun" "le" "i ndiaidh" "ar tí" "roimh" "<ina>";

# this type of verbal noun can be modified by attributive adj.
# e.g. "ag mothú tinn" but not "ag déanamh mór", "a bheith tanaí"
LIST SENSORY-VN = "<bheith>" ("mothú" Verbal) ("breathnú" Verbal) ("fáil" Verbal) ("aireachtáil" Verbal) "<éirí>";

# titles are nouns but often don't have gen. case on following noun
# some do ... e.g a thiarna easpaig
# e.g. an tUrramhach James, ár dTiarna Íosa
LIST TITLE = "urramach" "bantiarna" "tiarna" "uasal" ;
LIST DAYS = "Luan" "Máirt" "Céadaoin" "Déardaoin" "Aoine" "Satharn" "Domhnach" ;
LIST TIME = "mí" "bliain" "lá" "ráithe" "uair" "seachtain";
LIST TIME-PERIOD = "linn" "feadh" ;
LIST MEASURE = "slat" "orlach" "míle";
LIST THING = "ceann" "rud";

#GÓD Added imperative as verb with subj
LIST VERB-WITH-SUBJ = (vblex p1) (vblex p2) (vblex p3) (vblex aut) (vblex imp) ;

LIST PREP-ECL = "<i>" ; # etc. etc.
LIST PREP-LEN = "<de>" "<do>"; # etc. etc. 

LIST CUMANN-PRON = "<tú>" "<sé>" "<sí>" "<muid>" "<sibh>" "<siad>";
# Note: verb/noun pairs like "bac" are not rare in Irish. Thus,
# it makes perfect sense to put them into a list that might be
# extended as needed.
LIST NOUN-VERB-PAIR = "bac";
LIST UAIN-FEM-SURR = ("an" det def) ("aon" det qnt) ("céad" det ord len);
LIST NOUN-LEN = "ar" "de" "do" "faoi" "mar" "ó" "roimh" "trí" "um" ;

LIST VARIANT = "aimsear";
LIST GAOL = "deirfiúr";

# ==================================================================== #
# DISAMBIGUATION RULES
# ==================================================================== #
# ==================================================================== #
# SECTION 1 - Definite Rules
#SECTION
CONSTRAINTS
# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 IDIOMS
# -------------------------------------------------------------------- #
# Apertium: mw
# maille le e.g. maille le míle eile acu
# SELECT (pr) IF (0 ("<maille>")) (1 ("<le>"));

SELECT (vblex) IF (0 ("<raibh>")) (-1 (vpart));

# Apertium: mw
# as seo/sin amach
#SELECT (Pron Dem) IF (-1 ("as")) (1 ("amach"));
#SELECT (pr) IF (1 (Pron Dem)) (2 ("amach"));
#amach = AdvDir not Adj
#SELECT (adj) IF (-2 ("as")) (-1 (adj));

#amach = AdvDir not Adj

# Apertium: mw
# the after art case still needs handling, just not by this rule
#sé = Noun only in phrase "sé nó seachrán" or after Art
# REMOVE (n) IF (0 ("<sé>")) (NOT 2 ("seachrán"));

# Apertium: mw
# ann = Noun only in "in ann"
#REMOVE (n) IF (0 ("ann")) (NOT -1 ("<in>")) ;
#SELECT (n) IF (0 ("ann")) (-1 ("<in>")) ;

# ar/agus/ná a chumas/cumas/gcumas
#SELECT (n nom) IF (0 ("cumas")) (-2 ("ar") OR ("agus") OR ("ná")) (-1 ("a"));
# Replaced "a" with (det pos), e.g. "ar do chumas" - CW 09/08
SELECT (n nom) IF (0 ("cumas")) (-2 ("ar") OR ("agus") OR ("ná")) (-1 (det pos));

# mar gheall ar/air
SELECT (n nom) IF (0 ("geall")) (1 ("ar")) (-1 ("mar"));
SELECT (pr) IF (1 ("geall")) (2 ("ar")) (0 ("mar"));

# ar chor/cor ar bith
SELECT (n sg) IF (0 ("cor")) (1 ("ar")) (2 ("bith"));

# aon chor/cor
SELECT (n sg) IF (0 ("cor")) (-1 ("aon"));
SELECT (det qnt) (1 ("cor")) (0 ("aon"));

# ar mhaithe le
SELECT (Noun Sg Len) IF (0 ("maithe")) (-1C ("<ar>")) (1 ("le"));

# cé as a conjunction
#GÓD if not followed by question mark. 
SELECT (Conj) IF (0 ("cé")) (1 ("go") OR ("gur") OR ("nach") OR ("nár"))(NOT 1* ("?" sent));

# le cúnamh/cuidiú Dé
REMOVE (vblex subst) IF (0 ("cúnamh") OR ("cuidiú")) (1 ("<Dé>"));

# let us assume that ba = bó(pl) must have a pl article preceding it
# not strictly true but ba=cop in most cases ... 
REMOVE (n) IF (0 ("<ba>") OR ("<Ba>")) (NOT -1 (det def pl));

# go deimhin is usually adverbial; certainly
SELECT (part adv) IF (0 ("go")) (1 ("<deimhin>"));
SELECT (Adj Base) IF (-1 ("go")) (0 ("<deimhin>"));

# dar as a verb must be followed by prep "le", i.e. dar le = according to
# dar/V leis/léi/liom ,
SELECT (vblex) IF (0 ("<dar>") OR ("<Dar>")) (1 ("le"));
REMOVE (vblex) IF (0 ("<dar>") OR ("<Dar>")) (NOT 1 ("le"));

# dar as verb particle only occurs before a PastInd verb
# dar/Q tháinig/V-PastInd 
# but dar/!=Q díobh/V-Imper OR dar/!=Q dtaltaí/V-Ecl 
REMOVE (vblex) IF (-1 ("<dar>") OR ("<Dar>")) (NOT 0 (Verb PastInd));
REMOVE (vblex ecl) IF (-1 ("<dar>") OR ("<Dar>"));
REMOVE (vpart) IF (0 ("<dar>") OR ("<Dar>")) (NOT 1 (Verb PastInd));
SELECT (vpart) IF (0 ("<dar>") OR ("<Dar>")) (1 (Verb PastInd));

# dar as a copula occurs with prep pron "de" i.e dar díobh = was of them
SELECT (Conj Cop) IF (0 ("<dar>") OR ("<Dar>")) (1 ("de" Pron Prep));
# dar as copula occurs with Adj e.g. dar léir siúd
SELECT (Conj Cop) IF (0 ("<dar>") OR ("<Dar>")) (1C (adj));
# dar as copula occurs with go + Adj e.g. dar go deimhin
SELECT (Conj Cop) IF (0 ("<dar>") OR ("<Dar>")) (1 (part adv)) (2 (adj));

# dar mo chonsias...dar fia ... dar Crom ... dar an leabhair ... 
# before all else it must ne a prep simp ...
SELECT (pr) IF (0 ("<dar>") OR ("<Dar>")) (1C (n) OR NOUN-PREMOD);

# ar sise/seisean/
# dont use Prop Noun as too many many Prep + Placenames/Orgn.
SELECT (vblex) IF (0 ("<ar>") OR ("<Ar>")) (1 (subj));
REMOVE (vblex) IF (0 ("<ar>") OR ("<Ar>")) (NOT 1 (subj) OR ("mé"));

# a bheag nó/ná a mhór
SELECT (det pos) (1 ("mór")) (0 ("a"));
SELECT (n) (0 ("mór")) (-1 ("a"));

# Cop: is/ní dóigh
# Prep: ar/sa/ón ndóigh/dhóigh/dóigh
# Conj: ach/agus dóigh
SELECT (n) IF (0 ("<dóigh>") OR ("<dhóigh>") OR ("<ndóigh>")) (NOT 1 (subj)) (-1 (cop) OR (pr) OR (Conj));

# some De Names commonly found in the corpus ...
SELECT (Part Pat) (0 ("de")) (1 ("<Valera>") OR ("<Brún>") OR ("<hÍde>") OR ("<Niro>") OR ("<Róiste>") OR ("<Cléir>") OR ("<Spáinn>"));

# TYPE 1
# "ann" is only subst noun in phrase "in ann"
REMOVE (Subst Noun) IF (0 ("ann")) (NOT -1 ("i"));
SELECT (Subst Noun) IF (0 ("ann")) (-1 ("i"));

# "go bhfuil" is invariably verbal ...
SELECT (Verb PresInd Dep Ecl) (0 ("<bhfuil>")) (-1 ("<go>"));

# "rinne" is usually verbal ...
# an éileamh a rinne Hitler ...
SELECT (vblex) (0 ("<rinne>")) (-1 ("<a>")) (1C (n) OR NOUN-PREMOD);
SELECT (vblex) (0 ("<Rinne>")) (-1 BOS);

# "cruth an duille, leagan/!=VN amach na mbláthanna
# <leagan amach> to be made into MWE (multi word expression)
REMOVE (vblex subst) IF (0 ("<leagan>")) (1 ("<amach>"));

# a deir/!=noun ... usually
SELECT (vblex) (0 ("abair")) (-1 ("<a>"));
# Deir at start of sentence is more likely to be verb than noun
SELECT (vblex) IF (0 ("<Deir>"));

# the verb reading will only be removed for ambiguous items
# genuine unambiguous verbs will survive this rule
# e.g. " an cuid atá/V
# but " cuid mhaith/!=V
REMOVE (vblex) IF (-1 ("<cuid>"));

# Apertium: mw
# idiom scun scan/!=V
# SELECT (n) IF (0 ("<scan>")) (-1 ("<scun>"));

# i gcomhair, faoi chomhair, os comhair
SELECT (n) IF (0 ("comhair")) (-1 (pr));

# os ár comhair
SELECT (n) IF (0 ("comhair")) (-2 (pr)) (-1 (det pos));

# dála an scéil
SELECT (n) IF (0 ("dála")) (2 ("<scéil>"));

# mo/do/a dhála féin, ár ndála féin
SELECT (n) IF (0 ("dála")) (-1 (det pos));

# Apertium: mw
# mac léinn: Mac is not patronymic (part of name) if followed by "léinn"
# SELECT (n nom) IF (0 ("mac")) (1 ("<léinn>"));
#
# mac léinn: "léinn" is not a verb if it follows "mac" 
# SELECT (n gen) IF (-1 ("mac")) (0 ("<léinn>"));
#GÓD
REMOVE (ecl) IF (0 ("léann"))(-1 ("mac"));# mac léinn
REMOVE (len) IF (0 ("léann"))(-1 ("mac"));# mac léinn

# leath uair=Com a chloig, 
# vs. leath an ama=Gen
SELECT (det) IF (0 ("leath")) (1 (n nom));

# níos mó ama, níos lú ama ...
SELECT (n gen) IF (-1 ("<mó>") OR ("<Lú>"));

# ar mhaith leat; is maith liom etc.
SELECT (adj) IF (0 ("maith")) (-1 (cop)) (1 ("le")) ;
#GÓD
SELECT (cop cni) IF (0 COP-COND)(1 ("maith")); # Ba mhaith liom, b'fhearr liom

SELECT (cop) IF  (1 ("maith")) (2 ("le")) ;


# ==> 23/11/08
# ar éigin (éigean)
SELECT (pr) IF (0 ("<ar>")) (1 ("<éigin>") OR ("<éigean>"));

#GÓD Remove the lesser used reading í (Noun) island.
SELECT (prn obj) IF (0 ("<Í>") OR ("<í>"));
REMOVE (n) IF (0 ("os"))(1 ("ard"));# Labhair sé os ard.

#GÓD Clásal coibhneasta díreach/indíreach - Gramadach Gan Stró Éamonnn Ó Dónaill lth 132.
#cé, cad, céard + forainm réamhfhoclach is followed by an indirect relative clause.
#cé, cad, céard + relative particle a is followed by a direct relative clause.

#Not a relative pronoun in the following
REMOVE (vpart Rel Pro) IF (-2 ("<Cé>") OR ("<cé>") OR ("<Cad>") OR ("<cad>") OR ("<Céard>") OR ("<céard>"))(-1 (Pron Prep))(2* ("?" sent));
REMOVE (vpart Rel Pro) IF (-3 ("<Cé>") OR ("<cé>"))(-1 ("<áit>") OR ("<chaoi>") OR ("<dóigh>") OR ("<fáth>") OR ("<tslí>") OR ("<mhéad>"))(2* ("?" sent));
REMOVE (vpart Rel Pro) IF (-2 ("<Cén>") OR ("<cén>"))(-1 ("<áit>") OR ("<fhad>") OR ("<uair>") OR ("<chaoi>") OR ("<dóigh>") OR ("<fáth>") OR ("<tslí>"))(2* ("?" sent));
REMOVE (vpart Rel Pro) IF (-2 ("<Cá>") OR ("<cá>"))(-1 ("<háit>") OR ("<fhad>") OR ("<mhéad>") OR ("<huair>"))(2* ("?" sent));
REMOVE (vpart Rel Pro) IF (-1 ("<Cathain>") OR ("<cathain>") OR ("<Conas>") OR ("<conas>"))(1* ("?" sent));

#Not a copula in the following
SELECT (vpart Rel Indirect) IF (-3 ("<Cé>") OR ("<cé>"))(-1 ("<áit>") OR ("<chaoi>") OR ("<dóigh>") OR ("<fáth>") OR ("<tslí>"))(2* ("?" sent));
SELECT (vpart Rel Direct) IF (-2 ("<Cé>") OR ("<cé>"))(-1 ("<mhéad>"))(1* ("?" sent));
SELECT (vpart Rel Direct) IF (-2 ("<Cá>") OR ("<cá>"))(-1 ("<mhéad>") OR ("<huair>"))(1* ("?" sent));
SELECT (vpart Rel Direct) IF (-2 ("<Cén>") OR ("<cén>"))(-1 ("<fhad>") OR ("<uair>"))(1* ("?" sent));
REMOVE (vpart Rel Indirect) IF (-1 ("cathain") OR ("conas") OR ("mar") OR ("nuair"));
SELECT (vpart Rel Direct) IF (-4 ("<Cé>") OR ("<cé>"))(-3 ("chomh"))(-2 ("minic"))(-1 ("is"))(1* ("?" sent));
SELECT (vpart Rel Indirect) IF (-2 ("<Cén>") OR ("<cén>"))(-1 ("<áit>") OR ("<chaoi>") OR ("<dóigh>") OR ("<fáth>") OR ("<tslí>"))(2* ("?" sent));


# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 VERB 
# -------------------------------------------------------------------- #
# -------------- @ADDED CW 09/08 --------------

# Avoid that "fuil" (blood) gets mixed up with "bí"
# First case: "a bhfuil, nach bhfuil, ina bhfuil"
#SELECT (vblex) IF (0 ("<bhfuil>")) (-1 ("<nach>") OR ("<go>") OR (vpart rel indirect));
# The above one did not work properly.
# The cases are: an bhfuil, nach bhfuil, a bhfuil, lena, ina (etc) bhfuil, mura bhfuil, cá bhfuil
# Added CW 10/08
#TEMPLATE BI-TAIL = (-1 (vpart itg) OR (vpart itg neg) OR (vpart rel neg) OR (vpart rel indirect) OR (Prep Rel) OR (cnjsub) OR (adv itg));
#SELECT (vbser) IF (0 ("bí")) (T:BI-TAIL);
# TEMPLATELESS VERSION below.
SELECT (vbser) IF (0 ("bí" ecl)) (-1 (vpart itg) OR (vpart itg neg) OR (vpart rel neg) OR (vpart rel indirect) OR (Prep Rel) OR (cnjsub) OR (adv itg));
# Special case at the beginning of a sentence: "Bhfuil duine ar bith istigh"
# Note: Does not cater for "bhfuil duine..." or "a' bhfuil duine..." 
SELECT (vbser) IF (0 ("<Bhfuil>"));
# Second case: Munster Irish
SELECT (vbser) IF (0 ("<fuil>")) (-1 ("ná" Conj));
# Second case (although rare): "fuil nach fuil Éireannach í"
#REMOVE (vbser) IF (0 ("bí")) (NOT -1 ("<nach>" Cop) OR (vpart rel indirect) OR (Prep Rel));
REMOVE (vbser) IF (0 ("<fuil>")) (NOT -1 ("<nach>" Cop) OR (vpart rel indirect) OR (Prep Rel));

# Added CW 10/08
# Avoid that "raibh" is invariably misinterpreted as
# Noun Fem Sg ("floating seaweed") after "ní". More or less the same as
# in the case of "fuil".
#SELECT (Verb PastInd dep) IF (0 ("raibh")) (-1 ("<ní>", vpart neg) OR ("<Ní>" vpart neg));
# EUD 16/10/08 - needs testing ...
SELECT (vbser) IF (0 ("<raibh>")) (-1 (vpart));
SELECT (vbser) IF (0 ("<bhí>")) (NOT -1 (det def));
SELECT (vbser) IF (0 ("<Bhí>")) (NOT -1 (det def));
# TEMPLATELESS VERSION below.
#SELECT (Verb PastInd Dep) IF (-1 (vpart itg) OR (vpart itg neg) OR (vpart rel neg) OR (vpart rel indirect) OR (Prep Rel) OR (cnjsub) OR (adv itg));

# -------------- @END ADDED CW 09/08 --------------

SELECT (vblex) IF (1 (subj));
REMOVE (vblex aut) IF (1C (subj));

# TYPE 1
# it is a negative form of verb if it is preceded by a neg verb part
SELECT (vblex neg) IF (-1C (vpart neg));

# TYPE 1
# it is not a negative form of verb if it is not preceded by a neg verb part
# unless it is "níl"
REMOVE (vblex neg) IF (NOT -1 (vpart neg)) (NOT 0 ("<níl>") OR ("<Níl>"));

# neg particle for pres subj is nár or ná, i.e. a neg particle like ní implies that it is not pres subj
# note will deal with "ar mhaithe le" where "mhaithe" could be mistaken for a verb PresSubj rather than noun
REMOVE (vblex prs neg) IF ( NOT -1 ("ná") OR ("nár")) ;
REMOVE (vblex prs) IF ( NOT -1 ("ná") OR ("go")) ;

# it is a verb if it is followed by a pronoun form tagged as verb subject
# it is an interrogative form of verb if it is preceded by interr adv e.g. cá
# an bhfuil, an raibh, ar chuala
SELECT (vblex itg) IF (-1C (adv itg) OR (Part Q));
SELECT (vblex itg neg) IF (-1C (Part NegQ));

# it is not a neg. interog. form of verb if it is not preceded 
# by a neg interog. verb part
REMOVE (vblex itg neg) IF (NOT -1 (vpart itg neg));

# it is not an  interog. form of verb if it is not preceded 
# by a interog. verb part / interog. adv etc.
REMOVE (vblex itg) IF (NOT -1 (itg));

# e.g. rith sé amach: sé=verbsubj pron => rith = verb
# NOTE that rith can not be the imperative form
REMOVE (vblex imp) IF (1 (subj));	# 1 SEP

# neg particle for imperative form is "ná", not "níor" etc.
REMOVE (vblex imp neg) IF ( NOT -1 ("ná")) ;

# Ní fear a bhí ann ...
REMOVE (vblex imp) IF ( -1 ("ní")) ;

#Fear maith a bhí ann
REMOVE (vblex imp) IF ( *1 (vpart rel) BARRIER (n)) ;

# Fear maith is ea é 
REMOVE (vblex imp) IF ( *1 (cop) LINK 1 ("ea") BARRIER (n)) ;

# Ith PastInd Len not possible unless preceded by "do" verbal particle
# FIXME: is this necessary?
#REMOVE (Verb Vow PastInd) IF (NOT -1 (vpart)) ;
# 
# TYPE 2
# let us assume that imperatives are at the start of a sentence or that 
# they are preceded by some punctuation such as a quotation mark or comma
# Déan é. Ná déan é.
# A Sheáin, déan é.
# (a) Déan an rud seo
# ... agus déan an rud sin
REMOVE (vblex imp) IF (NOT -1 BOS OR (vpart Imp) OR (Punct) OR (Item) OR (cnjcoo))  ;

# TYPE 3
# 11 SEP
# where form is ambiguously n/v after prep and noun; select noun gen
# in Irish Chumann/N-gen Staire is Seandálaíochta Chiarraí ..
# in many cases the form is unambiguously a verb 
# e.g. ar maidin dúnann an t-ollmhargadh
REMOVE (vblex) IF (NOT 0 ("<rinne>")) (-1C NOUN-NOT-VN) (-2C (pr));

# Type 4 - true after disamb in earlier cycles
# Ba mhaith/V/N/Adj an rud ...
REMOVE (vblex) IF (-1C (cop));

# it cannot be a verb if followed by an unambigguous noun-gen
# "Meandar sula dtuigim nach bloscadh/!=verb toirní atá ann ...
REMOVE (vblex) IF (1C (n gen));

# liath/!=v nó bán
# fuaim ná fothram
REMOVE (vblex) IF (1C (Conj)) (NOT 2 (vblex) OR (vpart));

# -------------- @ADDED CW 09/08 --------------

# "réim" & "réimse"
# "réimse leathan de ghrúpaí" & "réim na Sasanach"
# were invariably misinterpreted as forms of the verb
# "réigh". Used a simple fix here that seems to work ok,
# for most cases, yet needs some further testing.
#REMOVE (vblex) IF (0 ("<réimse>")) (1 (n gen) OR (adj) OR (det def));
#REMOVE (vblex) IF (0 ("<réimse>")) (-1 (pr) OR (det pos) OR (Prep Poss) OR (det qnt def));
#REMOVE (vblex) IF (0 ("<réim>")) (1 (n) OR (adj) OR (det def));
#REMOVE (vblex) IF (0 ("<réim>")) (-1 (pr) OR (det pos) OR (Prep Poss) OR (det qnt def));
#REMOVE (vblex) IF (0 ("<réimse>")) (-2 ("bí") OR (-1 ("bí"));
#REMOVE (vblex) IF (0 ("<réim>")) (-2 ("bí") OR (-1 ("bí"));
# New version CW 10/08
REMOVE (vblex) IF (0 ("réigh")) (1 (n gen) OR (adj) OR (det def));
REMOVE (vblex) IF (0 ("réigh")) (-1 (pr) OR (det pos) OR (det qnt def));
REMOVE (vblex) IF (0 ("réigh")) (-1/-1 (pr)) (-1/-2 (det pos));
### CG3 -> REMOVE (vblex) IF (0 ("réigh")) (-2 ("bí") OR (-1 ("bí"));
REMOVE (vblex) IF (0 ("réigh")) (-2 ("bí"));
REMOVE (vblex) IF (0 ("réigh")) (-1 ("bí"));

#### END CHANGED CODE 


# Avoid that "bhás" is interpreted as the relative form of "báigh"
# This simple check is enough in this case, as "bhás" as 1P Past has
# not yet been encountered.
REMOVE (vblex) IF (0 ("bás")) (NOT -1 (vpart rel direct));

# Avoid that "níos" is interpreted as the relative form/ 1P past of "nigh"
REMOVE (vblex)  IF (0 ("<níos>")) (1 (adj) OR (adv));

# "fear" (man) vs. "fear" (grant, provide etc)
# If it is not in a structure like "d'fhear Peadar/sé/sí...", the 
# verb interpretation will be removed.
# This will erase most misinterpretations, as 'man' is much more
# common than the verb "fear".
REMOVE (vblex) IF (0 ("fear")) (NOT 1 (prn pers) OR (prn subj) OR (np));

# Avoid that "cumann" is interpreted as a form of "cum"
# in "Cumann na Talún", "cumann rugbaí", etc.
#LIST CUMANN-PRON = "<tú>" "<sé>" "<sí>" "<muid>" "<sibh>" "<siad>";
#TEMPLATE CUMANN-TAIL = (NOT 1 (CUMANN-PRON) OR (np) OR (det def));
# Simple assumption: If it starts with upper case 'C', it is part of a name,
# unless it is followed by personal pronouns.
# If proper nouns are added, "Cumann Merriman", will be misinterpreted.
REMOVE (vblex) IF (0 ("<Cumann>")) (NOT 1 (CUMANN-PRON));
REMOVE (vblex) IF (0 ("<Chumann>"));
REMOVE (vblex) IF (0 ("<gCumann>"));
# If it does not start with upper case 'c', remove Verb IF NOT:
# "cumann sé/Peadar/an file"
#REMOVE (vblex) IF (0 ("<cumann>")) (T:CUMANN-TAIL);
REMOVE (vblex) IF (0 ("<cumann>")) (NOT 1 (CUMANN-PRON) OR (np) OR (det def));
# "a chumann sé/Peadar/an file"
#REMOVE (vblex) IF (0 ("<chumann>")) (NOT -1 (vpart rel direct)); (T:CUMANN-TAIL);
REMOVE (vblex) IF (0 ("<chumann>")) (NOT -1 (vpart rel direct)) (NOT 1 (CUMANN-PRON) OR (np) OR (det def));
# "go gcumann sé/Peadar/an file", "a gcumann sé/Peadar amhráin di", "lena gcumann sé..."
# REMOVE (vblex) IF (0 ("<gcumann>")) (NOT -1 (vpart rel indirect) OR (Conj Sub) OR (Prep Rel)); (T:CUMANN-TAIL);
REMOVE (vblex) IF (0 ("<gcumann>")) (NOT -1 (vpart rel indirect) OR (Conj Sub) OR (Prep Rel)) (NOT 1 (CUMANN-PRON) OR (np) OR (det def));

#GÓD Remove initial mutation form if not preceded by a verbal particle
REMOVE (Verb Len) IF (Not -1 (vpart))(0 (Verb PresInd));
REMOVE (vblex ecl) IF (Not -1 (vpart))(0 (Verb PresInd));
######################################################################
# USE THIS INSTEAD IF YOUR VERSION OF VISL DOES NOT SUPPORT TEMPLATES
# (Comment out the line containing the template) - CW 10/08
######################################################################

# If it does not start with upper case 'c', remove Verb IF NOT:
# "cumann sé/Peadar/an file"
#REMOVE (vblex) IF (0 ("<cumann>")) (NOT 1 (CUMANN-PRON) OR (np) OR (det def));
# "a chumann sé/Peadar/an file"
#REMOVE (vblex) IF (0 ("<chumann>")) (NOT -1 (vpart rel direct)) (NOT 1 (CUMANN-PRON) OR (np) OR (det def));
# "go gcumann sé/Peadar/an file", "a gcumann sé/Peadar amhráin di", "lena gcumann sé..."
#REMOVE (vblex) IF (0 ("<gcumann>")) (NOT -1 (vpart rel indirect) OR (cnjsub) OR (Prep Rel)) (NOT 1 (CUMANN-PRON) OR (np) OR (det def));

# Added CW 10/08
# Avoid that "ar chumas" is interpreted as the past of "cum"
# As no case of "Ar chumas dán|amhrán?" or "an bhean ar chumas dán di" etc.
# could be found in the corpus, simply remove the verbal reading. The more
# careful approach, that is commented out, did not work, no matter where it was
# inserted as "ar" keeps all of its possible interpretations throughout the
# disambiguation. The careful approach will only work, if "ar" is filtered properly
# before this command, which turned out to be quite difficult.
#REMOVE (vblex) IF (0 ("<chumas>")) (NOT -1 ("ar" vpart past rel indirect) OR ("ar" vpart past itg));
REMOVE (vblex) IF (0 ("<chumas>")) (-1 ("ar"));

# -------------- @END ADDED CW 09/08 --------------

# póg ar a leiceann./!=verb
# BUT: eg ..sin a bhfuil.  ...Pól a d'aistrigh.
# Rule ??
# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 PRONOUNS 
# -------------------------------------------------------------------- #
# TYPE 3
# sé/sí/siad after a verb is usually a pronoun, unless followed by a noun 
# number etc.
# chuaigh sé amach vs. chuaigh sé fhear amach, chuaigh sé chéad fhear amach 
# thóg sé trí úll
SELECT (subj) IF (-1C (vblex)) (NOT 1 (n) OR NUM-COUNT);

# TYPE 3
# D'imigh sé leis; Bhailigh siad leo etc
# FIXME
SELECT (subj) IF (-1C (vblex)) (1 (Pron Prep));

SELECT (subj) IF (-1C (vblex)) (1 (prn));

# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 NOUN 
# -------------------------------------------------------------------- #

# -------------- @ADDED CW 09/08 --------------

# "beo" is a noun only in certain cases.
# This addition necessitated further changes in the
# adjectives section.
REMOVE (n) IF (0 ("beo")) (NOT -1 (pr));
#REMOVE (n) IF (0 ("beo")) (NOT -1 (Prep Poss));
REMOVE (n) IF (0 ("beo")) (NOT -1/-1 (pr)) (NOT -1/-2 (det pos));
REMOVE (n) IF (0 ("beo")) (NOT -1 (det pos));
REMOVE (n) IF (0 ("beo")) (NOT -1 (det def));

# "eile" is often misinterpreted as genitive of "eil" (ell).
# "eil" (meaning ell) could not be found in the corpus, though.
# The wordform "eil" is either found in Scottish Gaelic texts
# or a typo.
# The strategy is to remove the genitive interpretation, if it is not
# a 100 % case.
REMOVE (n) IF (0 ("eil")) (NOT -1 (det def f sg gen) OR (n));
# Include preceding noun also EUD Mar 2016

# Avoid that "trá" (beach) is mixed up with 
# "trá" (Lit: Then, indeed; however.)
# This is a simple check. If the word "trá" is not 
# embedded in the phrase "Cidh trá acht" (At any rate), we 
# dismiss the adverbial or conjunctional interpretation, since
# it is likely that this is the wrong interpretation. Everything
# else would be tiresome and error prone.
REMOVE (adv) IF (0 ("trá")) (NOT -1 ("<Cidh>") OR ("<cidh>"));
REMOVE (Conj) IF (0 ("trá")) (NOT -1 ("<Cidh>") OR ("<cidh>"));

# See S2 ADJECTIVES
# "loch sa tír a atá breac ballach le hoileáin"
REMOVE (n m) IF (0 ("breac")) (-1 ("bí")) (-2 (vpart rel));

# -------------- @END ADDED CW 09/08 --------------

# "an bhfuil" cannot be a noun unless preceded by preposition
# ar an bhfuil
REMOVE (n ecl) IF (-1 ("an")) (NOT -2 (pr));

# it cannot have the DefArt reading if not preceded by an Art (or Prep Art etc.)
REMOVE (n defart) IF ( NOT -1 (det def));

# Unlikely to have emphatic noun without possessive determiner
# mo theachsa, a ngalfchúrsa
REMOVE (n emph) IF ( NOT -1 (det pos));

# it cannot have the Dat reading if not preceded by a Preposition 
# Use Prep Simp eventhough there are some exceptions 
#e.g. Chraith chuile dhuine acu láimh/NDat leis : but in these cases the 
# only reading (e.g. for láimh) is Dat - and so will not be removed
# but this helps with dtig and cois where there are multiple readings
REMOVE (Noun Dat) IF (NOT -1 (pr));

# Dé Luain, Dé Sathairn etc Gen form follows Dé in days of week
SELECT (n) IF (0 ("Dé")) (1 DAYS);
REMOVE (n) IF (0 ("Dé")) (NOT 1 DAYS);
SELECT (gen) IF (-1 ("Dé" Subst));

# gach a raibh le déanamh, 
# "gach" is not a substantive if it is followed by a noun/det/num
# gach rud, gach aon rud etc.
SELECT (Subst Noun) IF (0 ("gach")) (1 (rel) OR (RelInd));
REMOVE (Subst Noun) IF (0 ("gach")) (NOT 1 (rel) OR (RelInd));

# ag ardú a chinn ... vs nuair a chinn sí ...
SELECT (n gen) IF (0 ("chinn")) (-1 (n));

# Verb "a" X; where X is either noun or verb choose noun; 
# e.g. Ní raibh a fear céile...; Bhí a leath i bhfolach ...
# Note: in majority of cases X is unambiguously a verb
# e.g. thuigfeadh a raibh i gceist; na postanna atá a gcailleadh
REMOVE (vblex) IF (-1 ("a")) (-2C (vblex));

# ina theannta/measc siúd vs. Déan siúd
SELECT (n) IF (-1 (pr)) (1 ("<siúd>"));

#  "ina" X; where X is either noun or verb choose noun; 
# e.g. ina measc, ina bhás, 
# Note: bhfuil is an exception
# Note: in majority of cases X is unambiguously a verb
SELECT (n) IF (-1 ("<ina>")) (NOT 0 ("<bhfuil>"));

############################
# Proper Nouns new section
###########################
SELECT (Prop Noun Masc Gen Sg) IF (-1 ("<Mac>") OR ("<Uí>") OR ("<Mhic>"));
SELECT (Prop Noun Fem Gen Sg) IF (-1 ("<Ní>") OR ("<Nic>"));
SELECT (Prop Noun Masc Dat Sg) IF (-1 ("<Ó>") OR ("<Ua>") OR ("<de>"));

# Proper Names
SELECT (n) IF (1 ("<Ó>") OR ("<Ní>") OR ("<Uí>")) (2 (np));
SELECT (n) IF (-1 ("<Ó>") OR ("<Ní>") OR ("<Uí>")) (-2 (np));
SELECT (Part Pat) (0 ("<Ó>") OR ("<Ní>") OR ("<Uí>")) (-1C (n)) (1C (n));

# disambiguate Len/Ecl tags on non-mutable initial vowel/cons
# i.e. vowels, l, n, r, etc.
# e.g. aicme laoch roinnt etc.

# a verb does not cause a following noun to be lenited or eclipsed
# e.g. bhíodh aicme laoch ...
REMOVE (n len) IF ( -1C (vblex));
REMOVE (n ecl) IF ( -1C (vblex));

# e.g. i roinn; this would be eclipsed if it were possible
SELECT (n ecl) IF (0C NOUN-NOT-VN) (-1C PREP-ECL);

# e.g. de roinn; this would be lenited if it were possible
SELECT (n len) IF (0C NOUN-NOT-VN) (-1C PREP-LEN);

# we should not have a gen noun form directly following a verb
REMOVE (n gen) IF (-1C (vblex));
REMOVE (n gen) IF ( -1C (det def)) (-2C (vblex));

# APR 2010
# we should not have a gen noun form directly following a simple prep
REMOVE (n gen) IF (-1C (pr));
REMOVE (n gen) IF ( -1C (det def)) (-2C (pr));

#GÓD Remove the genitive noun form if there is no preceding genitive assigner.
REMOVE (Noun Gen) IF (NEGATE -1* (Noun) OR GEN-SIMP-PREP OR GEN-PART OR (Prep Cmpd) BARRIER (vblex));
#GÓD
SELECT (Noun Com) IF (0 ("leabhar")); # Thug Seán Ó Broin leabhar do Mháire.
# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 ARTICLE 
# -------------------------------------------------------------------- #
# a = an is only allowed in time phrases such as "trí a chlog" etc.
REMOVE (det def sg) IF (0 ("<a>")) (NOT 1 ("clog"));
# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 POSSESSIVE DETERMINER 
# -------------------------------------------------------------------- #
# a != Det Poss if immediately preceded by definite NP
# an té a cheap, fear a sheas
# NOTE this does not take into account longer NPs with Adj etc.
# OR: "b'fhearr le na cailíní a/Det? leithéid"
# TEST this:
REMOVE (det pos) IF (NOT 1 (n));

# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 PREPOSITION
# -------------------------------------------------------------------- #
# a != Prep Simp unless followed by a Verbal Noun
# an té a cheap, fear a sheas
REMOVE (pr) IF (0 ("<a>")) (NOT 1 (vblex subst));
REMOVE (part inf) IF (0 ("<a>")) (NOT 1 (vblex subst));

# go != Prep Simp unless followed by a Noun
# go Meiriceá, go doran an tí, go 91 vóta
REMOVE (pr) IF (0 ("<go>")) (NOT 1 (n) OR NOUN-PREMOD);
SELECT (pr) IF (0 ("<go>")) (1C (n) OR (num) OR (det));

# TYPE 1
# go/PartAd is used with adjectives to form an adverb 
# e.g. go leor, go maith etc.
# "go" is tagged as Part Ad so that preps will only ever precede an NP
REMOVE (pr) IF (0 ("go")) (1C (adj));


# "a" before "chlog/cloig" is a prep
SELECT (pr) IF (0 ("<a>")) (1C ("clog"));
SELECT (pr) IF (0 ("<a>")) (1 (Punct)) (2C ("clog"));

# it is a prep. if followed by the dative case
# e.g. ó Éirinn
SELECT (pr) IF (1 (n dat));

# TYPE 1
# d'éirigh leo: "leo" is prep not noun
SELECT (pr) IF (0 ("le")) (-1 ("éirigh")); 

# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 COPULA 
# -------------------------------------------------------------------- #
# TYPE 1
# ea is only used with copula
SELECT (cop) IF (1 ("ea"));
#GÓD e.g. Cailín is ea í.
SELECT (prn) IF (-1 (cop));
# ba dh'ea
SELECT (cop) IF (1 ("<dh'>")) (2 ("ea"));

# TYPE 1
# féidir is only used with the copula
SELECT (cop) IF ( 1  ("féidir"));

# e.g. Ba iad, is é ... perhaps this is too broad
# an é , nach é, ní hí 
SELECT (cop) IF (1 ("é") OR ("í") OR ("iad") OR ("hé") OR ("hí") OR ("hiad"));

# Is/Ní gá: is/ní are most likely to be a copula if followed by "gá"
# but exclude "an" as this could be either Art or Cop e.g  an gá
SELECT (cop) IF (NOT 0 ("<an>")) (1 ("gá"));

# If it is at the start of a sentence Is is more likely to be Cop than Conj
SELECT (cop) IF (0 ("<Is>")) (NOT 1 (vblex) OR (vpart) OR (cop));

#Arbh é Seán a bhí ann - arbh!=dependant at start of sentence
REMOVE (Cop Dep) IF ( -1 (sent));

# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 PARTICLES - Verbal
# -------------------------------------------------------------------- #

# it cannot be a verbal particle if it is not followed by a verb
# or verb particle (Vb) such as d' e.g. a d'fhreagair Máire
REMOVE (vpart) IF (NOT 1 (vblex) OR (Vb));

# it cannot be a subjunctive verbal particle if it is not followed by a subj. verb
REMOVE (vpart Subj) IF ( NOT 1 (vblex prs));

# it cannot be an imperative verbal particle if it is not followed by an imper. verb
REMOVE (vpart Imp) IF ( NOT 1 (vblex imp));

# cé go/nár/gur/nach
# e.g. cé go raibh, 
REMOVE (vpart Subj) IF ( -1 ("cé"));

# it is a verb particle if it is followed by an unambiguous (C=careful) verb 
# except go=Conj e.g. Tá sé soiléir go raibh ... 
# go=verb part only in the case of subjunctives
#=>>>>> 25/06/08 go = vpart Cmpl (complementizer)
#=>>>>>SELECT (vpart) IF (1C (vblex)) (NOT 0 ("go"));
SELECT (vpart Subj) IF (1C (vblex prs));
SELECT (vpart) IF (1C (vblex));

#===>>> check this NegQ out ...
SELECT (vpart itg neg) IF (1C (vblex itg));
SELECT (vpart itg) IF (1C (vblex itg));

# Ní mór 
# Ní raibh
# but: níor glacadh
REMOVE (vpart neg) IF (0 ("ní")) (NOT 1 (Verb Len));

# nach bhfuil vs. nach raibh
REMOVE (vpart Past) IF (NOT 1 (PastInd) OR (PastImp) OR (PastSubj));

# d' before a possible verb is most likely to be a verb particle
# e.g. d'fhág is part + Verb rather than prep + noun
SELECT (vpart) IF (0 ("do")) (1 (Verb PastInd));

# "a" is an indirect relative, if the following verb is followed by
# poss det mo/do/a 
# e.g. Indirect: an fear a raibh a mhac san ospidéal
# Direct  : an fear a bhí san ospidéal
SELECT (vpart rel indirect) IF ( 1C (vblex)) (2 (det pos));

# verb is lenited after direct rel "a" and eclipsed after indir rel "a"
# use 1C here as "a" could be Prep Simp with Nv e.g. a dhíol
SELECT (vpart rel direct)   IF (0 ("a")) ( 1C (Verb Len)) ;
# Added CW 11/08
SELECT (vpart rel direct)   IF (0 ("<a>")) ( 1C ("do" vpart)) ;
# End Added CW 11/08
SELECT (vpart rel indirect) IF (0 ("a")) ( 1C (vblex ecl));
# DEPENDANT FORMS also follow rel particles
SELECT (vpart rel indirect) IF ( 1 (Verb Dep));
# Added CW 11/08
SELECT (part inf) IF (0 ("<a>")) (1 (vblex subst len)) (NOT 2 (n) OR (prn pers) OR (det def) OR (prn subj) OR (prn obj));
# End Added CW 11/08
# Ar shíl/inis/thomhais tú etc. where Verb/Noun ambiguity - choose verb if preceded by Ar at start of sentence (i.e. starts with cap)
SELECT (vpart Q Past) IF (0 ("<Ar>")) (1 (vblex)) (2 (prn pers)) (NOT 2 ("í") OR ("é"));

# -------------------------------------------------------------------- #
# S1 PARTICLES - Copula
# -------------------------------------------------------------------- #
# it can't be  a copular particle if it is not followed by a (possible) copula
REMOVE (Part Cp Rel) IF (NOT 1 (cop));

# -------------------------------------------------------------------- #
# S1 PARTICLES - Vocative
# -------------------------------------------------------------------- #
# it cannot be a vocative particle if it is not followed by a proper (voc) noun
# not all prop nouns are marked as such ... but all will have Voc
# but voc not always used ... e.g , a Tom.
REMOVE (Part Voc) IF ( NOT 1 (n voc) OR (np));

# "A dhiabhail" or "A bhó chairdiúil" 
#SELECT (Part Voc) IF (@1 (Part Voc)) (@2 (n voc)) (@3 (Punct) OR (n) OR (adj) OR ("<na>") OR (ij));
SELECT (Part Voc) IF (-1 BOS)  (1 (n voc)) (2 (Punct) OR (n) OR (adj) OR ("<na>") OR (ij));
SELECT (Part Voc) IF (-2 BOS) (-1 (ij) OR (Punct) OR (XMLTag))  (1 (n voc)) (2 (Punct) OR (n) OR (adj) OR ("<na>") OR (ij));
# , a Bheartla
# <p> A Mháire
# ' A Lia,
# Ó a Rí na trua, tóg dínn ...
# .. in this above example Ó is taken to be a preposition :-(
REMOVE (Part Voc) IF (NOT @1 (Part Voc)) ( NOT -1 (Punct) OR (XMLTag) OR (ij)) ;
SELECT (Part Voc) IF (-1 (ij)) (1 (n voc));
# it is a voc particle if it is followed by a proper noun
SELECT (Part Voc) IF (1 (Prop));

# ", a chroí" ", a mhac" etc
# BUT not if the noun could be a verbal noun
# e.g. ", a chothú...", ", a dheisiú..." ", a chodladh.."
# OR even a verb
# e.g. ", a shíl mé..."
# OR if followed directly be a functional category like Conj or Prep
# vocative examples:
# a dhuine cóir, a Bheartla, a Mhicil chroí, a ghiolla na léitheoireachta, 
# a chúil fáinneach na dtrioplaí siar, a iníon ó, a mhaca Uisnigh,
SELECT (Part Voc) IF (-1 (",")) (1 (n voc)) (NOT 1 (vblex subst) OR (vblex)) (2 (Punct) OR (n) OR (adj) OR ("<na>") OR (ij));

# -------------------------------------------------------------------- #
# S1 PARTICLES - Patronymic (names)
# -------------------------------------------------------------------- #
# it cannot be a patronymic particle if it is not followed by a proper noun
# soften the following rule to noun rather than proper noun
# some surnames are also common nouns
#REMOVE (Part Pat) IF ( NOT 1 (np));
REMOVE (Part Pat) IF ( NOT 1 (n));

# restrict de as Part Pat to cases where followed and preceeded by a proper 
# noun to avoid unnecessary ambiguity
REMOVE (Part Pat) IF (0 ("<de>")) ( NOT -1 (np)) (NOT 1 (np));

# Let us assume that in a name (Part Pat) Mac will have uppercase
REMOVE (Part Pat) IF (0 ("<mac>")) (NOT 1 (Prop)) ;

# it is a patronymic particle if it is followed by a proper noun
# but note that de is quite often a Prep before Prop noun
# e.g. 31ú lá de Nollaig
SELECT (Part Pat) IF (1 (Prop)) (NOT 0 ("de"));
SELECT (Part Pat) IF (-1 (Prop))  (0 ("de")) (1 (Prop));

# -------------------------------------------------------------------- #
# S1 PARTICLES - Numeric
# -------------------------------------------------------------------- #
# it cannot be a numeral particle if it is not followed by a numeral
# a ceathair a clog, Dé  Satharn ar a 4
REMOVE (Part Nm) IF ( NOT 1 NUM-COUNT OR (Num Dig));
# a chéad rogha 
REMOVE (Part Nm) IF ( 1C (Num Len) OR (Num Ecl));
SELECT (Part Nm) IF ( 1 (Num Card));

# -------------------------------------------------------------------- #
# S1 PARTICLES - Adjectival
# -------------------------------------------------------------------- #
# it cannot be an comparative or superlative particle if it is 
# not followed by a comp adj
# e.g. is mó, níos lú, ba mhó
# AND Verbal Adj   ...níos spreagtha ...
# e.g. ...beartais a sholáthar agus ionchur níos sonraithe/VA a áirithiú...
# BUT some Adj Comp are not recognised and are guessed as something else ...
# also ní ba mhó
REMOVE (Part Comp) IF ( NOT 1 (adj comp) OR (Verbal Adj) OR (cop));
REMOVE (Part Sup) IF ( NOT 1 (adj comp) OR (Verbal Adj));
REMOVE (Part Deg) IF ( NOT 1 (adj comp) OR (Verbal Adj));

# it cannot be an degree particle if it is not followed by a comp. adj
# or abstract noun and a rel particle
# e.g a géire (agus) a labhair sí, a dhonnacht (is) a bhí sé
REMOVE (Part Deg) IF ( NOT 1 (adj comp) ) (NOT 2 (rel) OR (Coord));

# select comparative or superlative particle if followed by Comparative Article
SELECT (Part Sup) IF (1  (adj comp));
SELECT (Part Comp) IF (1  (adj comp));
# ní ba mhó;
SELECT (Part Comp) IF (1  ("<ba>")) (2 (adj comp));

# -------------------------------------------------------------------- #
# S1 PARTICLES - Adverbial
# -------------------------------------------------------------------- #
# "go" is adverbial particle if followed by adjective
SELECT (part adv) IF ( 1  (adj));
# ==> 23/11/08
REMOVE (part adv) IF (NOT 1  (adj));

#GÓD
SELECT (adv) IF (0 ("<inné>"))(NOT -1 (Noun));
REMOVE (adv) IF (-1* (rel ind)); # Sin an teach a raibh sé ina chónaí ann.
# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 NUMERALS
# -------------------------------------------------------------------- #
# an chéad bhliain, sa chéad leath, den chéad uair
# a chéad phost
# mar chéad fhocal, but NOT faoi chéad
REMOVE (Num Ord) IF (0 ("<chéad>")) (NOT -1 (det def) OR (det pos) OR ("<mar>"));
SELECT (Num Ord) IF (0 ("<chéad>")) (-1 (det def) OR (det pos) OR ("<mar>"));
#=====>>>>>>
SELECT (Num Card) IF (0 ("míle")) (1C (num) OR (n) OR (Punct Int));
REMOVE (Num Card) IF (0 ("míle")) (-1 ("míle"));

# dhá chéad, 
# faoi chéad, 
# le linn chéad fiche bliain, ar feadh chéad bliain
# ceithre mhíle siar ón mbaile => míle = Noun NOT Num in this context
SELECT (Num Card) IF (-1C (num) OR TIME-PERIOD OR ("<faoi>")) (1 (n) OR (num));
# mar aon chéad
# BUT Tógann sé dhá uair a chloig ...
SELECT (Num Card) IF (1C (Num Card)) (NOT -1 ("sé"));
#GÓD
REMOVE (Prep Simp) IF (-1C (Prep Cmpd)); # Tar éis trí lá tháinig sé abhaile.
REMOVE (Num Card) IF (NOT -1C (VERB-WITH-SUBJ ))( 0 ("sé"))(NOT 1 ("bliain")); # Cheannaigh sé leabhar áit a bhí sé ar fáil.
REMOVE (Pron Pers 3P Sg Masc) IF (-1 (Pron Pers 3P Sg Masc)); # Tá sé sé bliana d'aois.
# an seachtú reisimint
REMOVE (Num Ord) IF (NOT -1 (det def) OR (det pos) OR (pr));
SELECT (Num Ord) IF (-1 (det def) OR (det pos) OR (pr));

# an dá cheann vs dá réir sin
REMOVE (Num Card) IF (0 ("<dá>")) (NOT -1 (det def));
SELECT (Num Card) IF (0 ("<dá>")) (-1 (det def));

# deoch nó dhó vs d'inis mé (féin) dhó
SELECT (Num Card) IF (0 ("<dhó>")) (-1 ("nó") OR ("faoi"));
REMOVE (Num Card) IF (0 ("<dhó>")) (NOT -1 ("nó") OR ("faoi"));

# deoch/com nó dó vs di nó dó
REMOVE (Num Card) IF (0 ("<dó>")) (-1 ("nó")) (-2 ("do" Pron Prep));
# i gceann nóiméid/gen nó dó
SELECT (Num Card) IF (0 ("<dó>")) (-1 ("nó")) (-2 (n));
# rud beag nó dó
SELECT (Num Card) IF (0 ("<dó>")) (-1 ("nó")) (-2 (adj)) (-3 (n));

# aon nó dó; deich nó dó dhéag
SELECT (Num Card) IF (0 ("<dó>")) (-1 ("nó")) (-2 (Num Card));

# a dó/Num a chlog vs ar fáil dó/Pn vs tar éis an dó/N vs ag dó/Vn
REMOVE (Num Card) IF (0 ("<dó>")) (NOT -1 ("a"));
SELECT (Num Card) IF (0 ("<dó>")) (-1 ("a"));

# na trí chineál rud vs a trí a chlog vs trí mheán
#REMOVE (Num Card) IF (0 ("<trí>")) (NOT -1 (det def) OR ("a"));
SELECT (Num Card) IF (0 ("<trí>")) (-1 (det def) OR ("a"));

#trí mhí, trí chinn, 
SELECT (Num Card) IF (0 ("<trí>")) (1 TIME OR MEASURE OR THING);

# níos mó ná deich ...
SELECT (Num Card) IF (3 ("<níos>")) (2 ("<mó>") OR ("<lú>")) (1 ("<ná>"));

# trí artairí, trí théamh
#REMOVE (Num Card) IF (0 ("<trí>")) (1 (n pl) OR (vblex subst));
REMOVE (Num Card) IF (0 ("<trí>")) (1 (vblex subst));

# an dó dhéag
SELECT (Num Card) IF (1 ("déag"));

#trí nó ceathair
SELECT (Num Card) IF (1 (Conj)) (2 (Num Card));

# a trí nó a ceathair
# an trí nó an ceathair
SELECT (Num Card) IF (1 (Conj)) (2 ("<an>") OR ("<a>")) (3 (Num Card));

# the simple case
# e.g. aon dream amháin ..
SELECT (Num Card) IF (0 ("aon")) (2 ("amháin"));
# NEED TO ACCOUNT FOR COMPLEX NPs HERE
# let us say that a clause is bounded by internal punctuation or the start of a PP
SELECT (Num Card) IF (0 ("aon")) (*1 ("amháin") BARRIER PUNCT-INT OR (pr)) ;
# remove Num reading for "aon" if there is not "amháin" somewhere to the right
# except " a haon ..."
# BUT ní raibh ach aon ceann mór, tiubh amháin fágtha.
REMOVE (Num Card) IF (0 ("aon")) (NOT *1 ("amháin") BARRIER PUNCT-INT OR (pr)) (NOT -1 ("a")) ;

# a sé/NumCard a chlog
SELECT (num card) IF (1 ("<a>")) (2 ("clog"));
# an dó chlog ar maidin
SELECT (num card) IF (1 ("clog"));
# a sé/NumCard a ' chlog
SELECT (num card) IF (2 (Punct)) (3 ("clog"));
SELECT (num card) IF (-1C (Part Nm));
#GÓD Bhí sé fhear sa theach. Sé cannot be a pronoun if followed by a lenited noun.
SELECT (num card) IF (0 ("<sé>")) (1C (n len));
#GÓD Bhí sé sé mhí san Aifric. Personal pronouns do not trigger eclipsis.
REMOVE (num card ecl) IF (-1 (prn pers));
# -------------- @ADDED CW 09/08 --------------

# Avoid that "seasca" is interpreted as plural of "seasc" (barren, unfruitful)
# "naoi déag seasca"
SELECT (Num Card) IF (0 ("seasca")) (-1 (Num Card) OR ("déag"));
# "naoi bpunt seasca"
SELECT (Num Card) IF (0 ("seasca")) (-2 (Num Card)) (-1 (n));
# "sé chomhalta is seasca"
SELECT (Num Card) IF (0 ("seasca")) (-3 (Num Card)) (-2 (n)) (-1 ("<is>"));
# "seasca euro"
SELECT (Num Card) IF (0 ("seasca")) (1 (n));

# -------------- @END ADDED CW 09/08 --------------

# ==================================================================== #
# -------------------------------------------------------------------- #
# S1 CONJUNCTIONS
# -------------------------------------------------------------------- #
# go (=that) before a verb is tagged as a  conj
# - not a preposition e.g. "agus go ndéanaidís ... ", go mba mhaith leo
# unless it is unambiguously the pres subjunctive
# 25/06/08
#=>>>>>SELECT (Conj) IF (0 ("go")) (1C (vblex) OR (cop)) (NOT 1C (vblex prs));

# e.g. má bhíonn &rl.
SELECT (Conj) IF (0 ("má") OR ("dá")) (1C (vblex));
# if má/dá at start of sentence they are invariably a Conj
SELECT (Conj) IF (0 ("má") OR ("dá")) (-1 BOS) (1 (vblex));

# ==================================================================== #
# SECTION 2 - More Definite Rules
#SECTION
CONSTRAINTS
# ==================================================================== #
# -------------------------------------------------------------------- #
# S2 ADVERBS
# -------------------------------------------------------------------- #
# intensifiers must be followed by an adjective
# e.g. breá te, sách ard 
REMOVE (Adv Its) IF (NOT 1 (adj));
SELECT (Adv Its) IF (1 (adj));

# Anois preceded by Prep is usually the Newspaper .. otherwise it is an adverb
SELECT (adv) IF (0 ("<Anois>")) (NOT -1 (pr));

# ag rith timpeall/Adv
SELECT (adv) IF (-1C (vblex subst));
# ==================================================================== #
# -------------------------------------------------------------------- #
# S2 ADJECTIVE
# -------------------------------------------------------------------- #
# only adjectives follow an intensifier 
# e.g. breá te, sách ard, chomh maith 
SELECT (Adj Base) IF (-1 (Adv Its)) ;

# -------------- @ADDED CW 09/08 --------------
# See S1 NOUN
# A rare case, only five hits in the corpus.
# Also added "beocha" to the lexicon "adj-lex-stems5.txt"
# too unrestricted EUD Mar 2016
#SELECT (adj comp) IF (0 ("beo")) (NOT -1 ("<níos>"));
#SELECT (adj comp) IF (0 ("beo")) (NOT -2 ("<ní ba>"));

# See S1 NOUN
# Avoid that breac is interpreted as adjective "fuair sé breac"
REMOVE (adj) IF (0 ("breac")) (-1 (prn pers) OR (prn subj)) (-2 (Verb VT));
REMOVE (adj) IF (0 ("breac")) (-1 (Verb VT));

# Avoid that "úire" is interpreted as genitive feminine of Adj "úr"
SELECT (adj) IF (0 ("<úire>")) (-1 (n f sg gen));
# Avoid that "úir" (Adj Gen Masc) is interpreted as the noun "úir" (earth, soil)
SELECT (adj) IF (0 ("úir")) (-1 (n m sg gen));

# -------------- @END ADDED CW 09/08 --------------

# níos is only used before comparative form of adjectives
SELECT (adj comp) IF (-1 ("<níos>")) ;
# ní ba mheasa, ní b´áille
# NOTE: {ní ba} is part of the tokenizer, so this will not have
# any effect. - CW 09/08
SELECT (adj comp) IF (-2 ("<ní>")) ;

# adjectives follow a noun/pron (tháinig/v fear/n mór/a..., tá/v sé/pn mór/a) 
# another adj (fear/n mór/a ramhar/a)
# a comma (fear/n mór/a, ramhar/a, saibhir/a)
# an intensifier (sách te)
# adverbial "go" (go maith)
# copula "is/cop maith/adj liom "
# ag mothú/SENSORY tinn
# conjunction
# buan agus lán-aimseartha
REMOVE ADJ-NOT-VA IF (NOT -1 NOUN-NOT-VN OR (prn) OR (adj) OR (Adv Int) OR (part adv) OR COMMA OR (cop) OR SENSORY-VN OR (Conj));

# PrepPron cant be followed by Adj other than Verbal Adj ??
# an cuid is mó acu déanta ... acu  
REMOVE ADJ-NOT-VA IF (-1 (Pron Prep));

# it is not an attributive adj (i.e. inflected) unless preceded by a noun 
# or another adj 
REMOVE ADJ-ATTR IF ( NOT -1 (n) OR ADJ-ATTR);

# it is not a comparative adj unless preceded by a comparat. or superl. part.
# or degree particle (e.g. a géire ) 
# or "ba shéimhe... 
REMOVE (adj comp) IF ( NOT -1 (Part Comp) OR (Part Sup) OR (Part Deg) OR (cop));

# NOTE: adj can be followed immediately by a verb 
# e.g. Nuair a bhí an poll lán(adj) dhéantaí (vblex) é a chlúdach 

# it is not an adj if it is preceded immediately by an unambiguous verb in
# declarative clauses - but this does not hold for relatives
# e.g. "...an líne a/rel bhí/verb díreach"
# e.g. "Sin fear atá/rel-verb cliste ...
# Tá buailte agam ar an bhfear sin
# ...tá ceangailte orm dul agus an éagóir sin a chosc ...
# therefore use lemma bí rather than atá
#REMOVE (adj) IF ( -1C (vblex)) (NOT -1 ("<atá>")) (NOT -2 (Part Rel));
REMOVE (adj) IF ( -1C (vblex)) (NOT -1 ("bí")) (NOT -2 (Part Rel));

# Adj should match number of previous noun (unless numbers are involved)
# avoid the tagging "ainmneacha" as Adj in following "a lán ainmneacha"
# dhá(num) bhád(n-sg) bheaga(a-pl)
# trí(num) long(n-sg) déag(n-sg) mhóra(a-pl) (NIG p78)
REMOVE (adj sg) IF ( -1 (n pl));
REMOVE (adj pl) IF ( -1 (n sg)) (NOT -1 ("déag")) (NOT -2 (num)) ;

# for the case of vowel ending adjs which now only have Base and Base Len 
# le galar nua
REMOVE (Adj Base Len) IF (-1C (n)) (NOT -1 (n len) OR (n f) OR (Noun Masc Gen));

# Adjective should match noun in gender, number and case
# of the prev noun: exclude the PART-GEN nouns e.g. roinnt, cuid etc
# let us assume that they are followed by Noun Gen rather than Adj
# - this more likely but not essential ...

SELECT (adj f) IF (0C ADJ-NOT-VA) (-1C (Fem))  ;
SELECT (adj m) IF (0C ADJ-NOT-VA) (-1C (Masc)) ;
SELECT (adj sg) IF (0C ADJ-NOT-VA) (-1C (Sg)) (NOT -1 ("déag")) (NOT -2 (num))  ;
SELECT (adj pl) IF (0C ADJ-NOT-VA) (-1C (Pl))  ;
SELECT (adj pl) IF (0C ADJ-NOT-VA) (-1C ("déag"))  ;
SELECT (adj pl) IF (0C ADJ-NOT-VA) (-2 (num))  ;
SELECT (adj nom) IF (0C ADJ-NOT-VA) (-1C (Com) OR (Dat))  ;
SELECT (adj gen) IF (0C ADJ-NOT-VA) (-1C (gen))  ;
SELECT (adj voc) IF (0C ADJ-NOT-VA) (-1C (voc))  ;

# buama/n sách/Its éasca/a ..
SELECT (adj f) IF (0C ADJ-NOT-VA) (-1 (Adv Its)) (-2 (Fem))  ;
SELECT (adj m) IF (0C ADJ-NOT-VA)(-1 (Adv Its))  (-2 (Masc)) ;
SELECT (adj sg) IF (0C ADJ-NOT-VA)(-1 (Adv Its))  (-2 (Sg))  ;
SELECT (adj pl) IF (0C ADJ-NOT-VA)(-1 (Adv Its))  (-2 (Pl))  ;
SELECT (adj nom) IF (0C ADJ-NOT-VA)(-1 (Adv Its))  (-2 (Com) OR (Dat))  ;
SELECT (adj gen) IF (0C ADJ-NOT-VA)(-1 (Adv Its))  (-2 (gen))  ;
SELECT (adj voc) IF (0C ADJ-NOT-VA)(-1 (Adv Its))  (-2 (voc))  ;

# 28 AUG added C
# select the comparative if preceded by "is" or "níos" or "a"
SELECT (adj comp) IF (-1C (Part Sup) OR (Part Comp) OR (Part Deg));
SELECT (adj comp) IF (-2C (Part Comp)) (-1 (cop));

# e.g. na hAifrice Thuaigh ...
# but not roinnt airgid
# but no ar/N éigin/A => -1C 
SELECT (adj) IF (-1C (n) OR (adj) OR COMMA) (NOT -1 GEN-PART) ;

# TYPE 4 Rule - after disamb
# adjectives used adverbially
# select adj if preceded by adverbial particle, 
# e.g. go maith, go daingean etc...
SELECT (adj) IF (-1C (part adv));

# Bá mhór, ní mór, nach mór etc
SELECT (adj) IF (0 ("mór")) (-1 (cop));

# Is iontach, (like Is breá)
REMOVE (Adj Len) IF (0C (adj)) (-1C (Cop Pres));

# "Ba láidir" like "Ba mhaith", 
SELECT (Adj Len) IF (0C (adj)) (-1C (Cop Past));

# comparative adjective and degree particle are always followed by a relative clause
# a géire a labhair sí, 
# ba ea a dhéine a bhí sé á breathnú ...
SELECT (adj comp) IF (-1 ("<a>")) (1 (rel)) (2C (vblex));
# a chiúine is a bhí sé
SELECT (adj comp) IF (-1 ("<a>")) (1 (Coord)) (2 (rel)) (3C (vblex));
# a riachtanaí atá sé
SELECT (adj comp) IF (-1 ("<a>")) (1 ("<atá>")) ;
# a thabhachtaí is atá sé
SELECT (adj comp) IF (-1 ("<a>")) (1 (Coord)) (2 ("<atá>")) ;
# a indéanta agus a úsáidtí atá
SELECT (adj comp) IF (-1 ("<a>")) (1 (Coord)) (2 ("<a>")) (3 (adj comp)) (4 (rel));
# ==================================================================== #
# -------------------------------------------------------------------- #
# S2 VERBS  
# -------------------------------------------------------------------- #
# a verb follows relative particles like "inar (Prep Rel) dhiúltaigh (vblex) sé" 
# and "a (Vb Rel) deir (vblex)"
# but not "inar ghearr (n) go raibh sé"
# note "ar/cop mhaithe/n le" mhaithe = V presSubj should already be dealth with
# as not preceded by go or nár
SELECT (vblex) IF (-1 (Prep Rel)) (NOT 1 (vpart) OR (vblex) OR ("<go>"));

#####FIX THIS ~~~~
# na dátaí a ghabhann/V leo ... vs. as salann a dhíol/VN.
# BUT:  as salann a dhíol sa cheantar etc.
SELECT (Verb Len) IF (-1 (vpart rel direct)) (NOT 1 (Punct Fin));
SELECT (vblex ecl) IF (-1 (vpart rel indirect)) (NOT 1 (Punct Fin));

# Avoid that "ar chumas" is interpreted as the past of "cum"
#REMOVE (vblex) IF (0 ("<chumas>")) (NOT -1 ("ar" vpart past rel indirect) OR ("ar" vpart past itg));

# it is not a verb if it is followed immediately by "í", "é", "iad" or their emphatic forms, and verb is not a synthethic verb form (i.e. includes 
# person/number)
# e.g. can't have Tá iad ... 
# Unless verb is imperative, e.g.  Déan é
# NOTE relative constructions 
# e.g a/Rel fhorlíonann/V é "...agus mise a/Rel chonaic/v é/obj..."
REMOVE (vblex) IF (NOT 0 VERB-WITH-SUBJ OR (Imper)) (1 OBJ-PRON)
(NOT -1C (rel));

# remove reading of "ar" as a verb (e.g. , ar Seán. ) except where preceded
# by a quotation mark or comma 
# this may not always be true but it prevents the unlikely verb reading from 
# constantly appearing ...
REMOVE (Verb PastInd) IF (0 ("ar")) (NOT -1 (Punct Int) OR (quot)) ;

# a verb is not usually preceded by a noun/pronoun/art
# BUT sula rith siad/Pron d'fhéach/Verb siad thart
# '... óna chaint/n thuigeas/v go raibh ...
# e.g. "chuir siad deireadh go deo -> deireadh not verb
# na cinn chloiche -> cinn not verb
#REMOVE (vblex) IF (-1 (prn) OR (n) OR (det def));
REMOVE (vblex) IF (-1C (det def));

# if it could be autonomous (unspecified person) and is not followed by a pron or noun 
# then select autonomous reading
# transitive verbs need an obj NP e.g. 40,000 laoch
# e.g. Maraíodh 40,000 laoch "40,000 warriors were killed": killer not specified
#SELECT (vblex aut) IF (0C (vblex)) (NOT 1 (n) OR (prn) OR NOUN-PREMOD);

# it is a not a verb  if preceded by an unambiguous prep (C=careful mode)
# tháinig siad chun cinn -> cinn is not a verb in this context
# note "inar (Prep Rel) dhiúltaigh (vblex)"
REMOVE (vblex) IF (-1C (pr)) (NOT -1 (Prep Rel));

# it is probably not a verb if preceded by an unambiguous verb (C=careful mode)
# d'fhéadfadh líon mór tithe ... -> líon is not a verb in this context
REMOVE (vblex) IF (-1C (vblex));

# it is probably not a verb if followed by an unambiguous verb (C=careful mode)
# forbairt nó táirgeadh/!=v atá; Moltaí/!=v atá uaim
# BUT: Sna blianta a lean/v chuir/v sí go mór le litríocht
# mar dá dtiocfadh bheadh an sagart
# ach má phrioc thug sé priocadh chomh géar leis uaidh
REMOVE (vblex) IF (1C (vblex)) (NOT -1 ("a") OR ("má") OR ("dá"));

#it is probably not a verb if followed by an unambiguous adj
# e.g. líon beag tithe ... -> líon is not a verb in this context
REMOVE (vblex) IF (1C (adj));

# "aon" is usually followed by an NP not a verb (the "ace" meaning is not
# very common ...)
# e.g. "ní raibh aon cheapadh/N agam go dtabharfainn cuairt ar an áit 
# "Is é Pádraic Ó Conaire an t-aon fhear/N a raibh an t-eolas aige .
# but NOTE "Taca a ceathair thagadh/V an saoiste chugainn"
# AND "Fan go gcloisimid é dhá shéanadh."
# so we not extend this rule to Num Card in general
REMOVE (vblex) IF (-1 ("<aon>") OR ("<t-aon>"));
# TO BE TESTED
# "Ní raibh aon briseadh anseo"
REMOVE (vblex subst) IF (-1 ("<aon>") OR ("<t-aon>"));

# If a verb reading is possible and there is no verb to the left
# or if there is no verb to the right (looking no farther than a relative particle) .. then select the verb reading
# e.g. "(a) Déan liosta .. " here we want to select verb rather than noun reading for déan
# Ní mór dúinn - mór is not a verb here as preceded by Ní/Cop
# Ní fear aon leabhar a bhí ann ...
#NOTE Níor rith, Níor glacadh
SELECT (vblex) IF (NOT *-1 (vblex)) (NOT *-1 (cop)) (NOT *1 (vblex));

#====>>>> TEST THIS
# ná beadh, etc.
SELECT (vblex cni) IF (0C (vblex)) (-1 ("ná"));

# labhair go soiléir OK but not labhair sé/Séan/an fear etc
SELECT (vblex imp) IF (NOT 1 (n) OR (prn pers) OR (prn subj) OR NOUN-PREMOD);
REMOVE (vblex imp) IF (1C (np));
# -------------------------------------------------------------------- #
# S2 VERBAL NOUNS
# -------------------------------------------------------------------- #
# a VN following "ag" is not followed by attrib adj 
# unless it is a sensory vn
# e.g. "ag dlí (n)  poiblí (adj)" but "ag mothú(vn) tinn (adj)"
REMOVE (vblex subst) IF (-1 ("<ag>")) (NOT 0 SENSORY-VN) (1C ADJ-NOT-VA);

# verbal noun must be lenited after "a"
# a dhéanamh(VN?) vs a déanamh (!=Vn)
# also Ecl: "...ní raibh ionsaithe a ndéanamh orthu..."
REMOVE (vblex subst) IF (-1 ("<a>")) (NOT 0 (vblex subst len) OR (vblex subst ecl) OR (Guess Verbal Noun));

# e.g "agus a rá" where choice of rá=VN/VN_Len/VN_Ecl remains, choose VN_Len
#SELECT (vblex subst len) IF (-1C ("a" pr)) (0C (vblex subst));
# Removing 'C' produces the right results for VN. - CW 09/08
#SELECT (vblex subst len) IF (0 (vblex subst)) (-1 ("a" pr));

SELECT (vblex subst len) IF (-1 ("<a>" part inf));

# a dtaithí, a ndéanamh: "a" is Det Poss rather than Simp Prep ...
REMOVE (vblex subst ecl) IF (-1C ("<a>" Det Poss 3P Pl)) ;

# NEEDS TO BE TESTED MORE 
# ar crochadh/VN ar siúl/VN vs. ar chrochadh/N
# there are many mistakes/exception made e.g. ... taithí ar chóitseáil spóirt ina bhuntáiste 
REMOVE (vblex subst len) IF (-1C ("<ar>"))  (NOT 0 ("<bheith>"));

# if one of the possible readings is Verbal Noun and it follows a simple prep 
# - then select this reading
# but not a dtaithí where a/DetPoss - see previous rule ...
SELECT (vblex subst) IF ( -1 PREP-VN OR (part inf)) (NOT 0 (Verbal Noun Gen));
#==> 23/11/08
# deoch a ól - select Lenited VN reading of ól
SELECT (vblex subst len) IF ( -1 (part inf)) (NOT 0 (Verbal Noun Gen));

# verbal noun follows "á" - lemma "do" (a)
SELECT (vblex subst) IF ( -1 ("<á>")) ;

# bheith is almost invariably a verbal noun 
# e.g. ..ar a chumas bheith páirteach ...
SELECT (vblex subst) IF (0 ("<bheith>")) ;

# e.g. bain úsáid as -> úsáid = common noun
# e.g. sa (=ins an) troid
# NOTE i mbun an fhiosraithe/VNgen
# NOTE fear a/rel theastaigh/v fanacht/vn ...
# ... nach/rel bhféadfaí/v glacadh/vn leis na rialacháin ...    
REMOVE (vblex subst) IF ( -1 (vblex) OR (Prep Art)) (NOT -2 (rel)) ;

# a verbal noun is (often) preceded by a verb or copula, 
# therefore remove VN reading otherwise
# e.g. Roinnt bliain ó shin ... roinnt is not VN in this context 
# BUT SEE Seasamh amuigh i lár an bhóthair ... => use ADV amuigh
# NOTE Ag fanacht/vn sa bhaile a bhí/v sé.
# Ag tuairisciú don Chomhairle, táimid freagrach as ...
# Cén mhaith caint/VN leis go nochtfaidh/V ...
REMOVE (vblex subst) IF (NOT *-1 (vblex) OR (cop) ) (NOT *1 (rel) OR COMMA OR ("go") LINK 1C (vblex)) (NOT 1 (adv));

# VN doesnt start a sentence without some prep before it ????
# Rith/!=VN siad amach
# Ag rith/VN amach dóibh chonaic siad ...
# but can start a clause: "Éilófar uirthi, afách, taisteal/VN chuig an Ionad... 
REMOVE (vblex subst) IF (-1 BOS);
# -------------------------------------------------------------------- #
# S2 NOUNS
# -------------------------------------------------------------------- #
# APR 2010
#D´fhág !=Noun
REMOVE (n) IF (-1C (vpart));

# GÓD remove Noun Gen reading if it preceedes an infinitival particle e.g. tar éis méanfach a ligean
REMOVE (Noun Gen) IF (1 (Part Inf));

# TYPE 1/2
# nouns are not eclipsed after "go" 
# eclipsed noun only in phrases like "scian go mbuaidh" where go means "with"
# but this an exception
REMOVE (n ecl) IF (-1 ("go"));

# eliminate the unlikely leo=noun rather than common leo=prep-pron
# unless preceded by a poss det
# note can have "an leo é?" where an could be mistaken for the article ...
REMOVE (n) IF (0 ("<leo>")) (NOT -1 NOUN-PREMOD);

# it cannot have the DefArt tag if it is not preceded by the appropriate article
REMOVE (Noun Pl DefArt) IF ( NOT -1 (det def pl));
REMOVE (Noun Sg DefArt) IF ( NOT -1 (det def sg));

# remove Noun Gen reading if it follows a verb
# Diúltaíonn (na) mná tí ...
REMOVE (n gen) IF ( -1C (vblex));
REMOVE (n gen) IF ( -1C (det def)) (-2C (vblex));

# Cheannaigh Seán leabhar!=Gen i measc na sluaite=Gen
REMOVE (n gen) IF ( -2C (vblex)) (-1C (n nom))
(NOT *1 (n nom) BARRIER CLB) ;

# if gen pl is preceded by an article it must be a pl art
# remove Noun Gen Pl reading if it follows "an"
# Shín an sáirsint/n an guthán/n dearg chuig Dunne 
REMOVE (n pl gen) IF ( -1 ("<an>") OR ("<An>"));

# gen doesnt follow titles e.g. an tUrramhach James
REMOVE (n gen) IF ( -1C TITLE);
SELECT (n) IF ( -1C TITLE);

# it cannot be a vocative noun if it is not preceded by a voc particle
REMOVE (n voc) IF ( NOT -1 (part voc));
SELECT (n voc) IF ( -1C (part voc));

# can have Art Noun Noun 
# e.g. chonaic an fear bean
# e.g. An Roinn Oideachas, Na Gardaí Síochána 

# NOTE: siopa mór Sheáin N A N-gen
# it cannot be a genitive noun if it is not preceded by a 
# noun or verbal noun or compound prep or some simp preps (see LIST GENITIVE)  or noun+article
# ag baint as aimhréidhe
# added second Prep Cmpd
# e.g. i mbun an fhiosraithe ...
# Det Poss: tar éis a/detPoss mbreithe/gen
# Conj Coord: Cumann Seandálaíochta/gen is/conj Staire/gen: OMITTED for the moment as it causes too much ambiguity ..."chaith a chloigeann san aer agus rinne/!=gen gáire ...
# e.g. Is iomaí Gael/!=gen ...

# imithe chun/gen_part a/DetPoss pósta/gen
SELECT (n gen) IF (-1 (det pos)) (-2C GEN-SIMP-PREP);

# saolré na Chéad Dála
REMOVE (n gen) IF (NOT -1 GEN-SIMP-PREP OR GEN-PART OR (pr) OR (n) OR (adj) OR (det def) OR (det pos) OR (det ord)) ;

# remove noun-gen if there is not a noun or compound prep etc. somewhere to the left
# noun should be in gen following certain preps and partitive nouns
# therefore remove common and voc cases ...
REMOVE (n voc) IF (-1 GEN-SIMP-PREP OR GEN-PART OR (pr)) ;
REMOVE (n nom) IF (-1 GEN-SIMP-PREP OR GEN-PART OR (pr)) (NOT -1C (Guess))  ;

# leis: noun or prep pron?
# remove the noun reading of "leis" unless it is obviously a noun
REMOVE (n) IF (0 ("<leis>")) (NOT -1 NOUN-PREMOD);

# dó: noun or prep pron?
# remove the noun reading of "dó" unless it is obviously a noun
REMOVE (n) IF (0 ("<dó>")) (NOT -1 NOUN-PREMOD) ;

# sí: noun or pronoun?
# remove the noun reading of "sí" unless it is obviously a noun
REMOVE (n) IF (0 ("<sí>")) (NOT -1 NOUN-PREMOD);

# úd: noun or determiner?
# remove the noun reading of "úd" unless it is obviously a noun
REMOVE (n) IF (0 ("<úd>")) (NOT -1 NOUN-PREMOD);

# dul: noun or verbal noun?
# remove the noun reading of "dul" unless it is obviously a noun
REMOVE (n) IF (0 ("dul")) (NOT -1 NOUN-PREMOD);

# 15 SEP
# roinnt: noun or verbal noun?
# remove the noun reading of "roinnt" if not followed by a noun
# rud le roinnt/vn vs le roinnt/n míonna
REMOVE (NOUN-NOT-VN) IF (0 ("roinnt")) (-1 (pr)) (NOT 1 (n));

# céanna: noun or adj?
# céanna/chéanna (almost) always an adj
# gcéanna is noun in "mar an/a gcéanna"
# remove the noun reading of "céanna" unless it is obviously a noun
REMOVE (n) IF (0 ("céanna")) (NOT -1 (det def sg));
SELECT (n) IF (0 ("céanna")) (-1 (det def sg));
SELECT (det def sg) IF (1 ("céanna"));

# -------------- @ADDED CW 09/08 --------------

# Single out genitive in phrases like "ag déanamh iontais"
# But: If followed by an adjective, it is not genitive, i.e.
# "ag déanamh rud maith" (Could be moved up)
SELECT (n gen) IF (-1 (vblex subst)) (-2 ("<ag>")) (NOT 1 (adj)) (NOT 0 ("ó"));

# "féan" vs "féin"
# To avoid that the pronoun "féin" is invariably recognized as
# genitive singular or nominative plural of "féan" remove this
# interpretation, if it is not preceded by a definite article
# The wordform "féan" is only found six times in the whole corpus.
REMOVE (n m sg gen) IF (0 ("féan")) (NOT -1 (Art Sg Def));
REMOVE (n m pl nom) IF (0 ("féan")) (NOT -1 (Art Pl Def));

# "cun" (ns & gs) meaning stoutness, thickness
# Remove this interpretation, if not preceded by a definite article.
# So far, not a single occurrence of "cun" could be found in the corpus.
REMOVE (n) IF (0 ("<chun>")) (NOT -1 ("<an>"));

# The genitive of "bail" (prosperity) gets mixed up
# with "baile" (home, town), e.g. sa bhaile.
# The simplest solution: Remove "bail" if it is not a 100 % case of
# genitive as it is more likely to be "baile".
REMOVE (Noun Fem Gen) IF (0 ("bail")) (NOT -1 (Art Gen Sg Def Fem));
# Once "bail" is out of the way, determine the form of "baile".
# "sa bhaile", "insa bhaile" etc.
SELECT (n m sg nom) IF (0 ("baile")) (-1 (pr));
# "ar an bhaile". This is to cater for the Ulster version with aspiration
# which would otherwise be interpreted as genitive.
SELECT (n m sg nom) IF (0 ("baile")) (-1 (det def)) (-2 (pr));
# Genitive of "baile": "madraí an bhaile" or "muintir bhaile bheag"
SELECT (n m sg gen) IF (0 ("<bhaile>")) (-1 (det def));
SELECT (n m sg gen) IF (0 ("<baile>")) (-1 (n));
# "ar a (m)b(h)aile"
SELECT (n m sg nom) IF (0 ("baile")) (-1 (det pos)) (-2 (pr));
# "faoina (m)b(h)aile"
SELECT (n m sg nom) IF (0 ("baile")) (-1 (pr)) (-1/1 ("a" det pos));

# "tí" vs "teach", i.e. "ar tí" & "bean an tí"
# "bean an tí"
REMOVE (n f) IF (0 ("tí")) (-1 (det def));
# "ar tí" (without aspiration)
# Note: "go dtí" is lexicalized, so the preposition can be specified.
SELECT (n f nom) IF (0 ("<tí>")) (-1 ("<ar>"));
# Note:
# The 1st meaning of "tí" according to FGB is only used in lit. contexts
# and has so far not been found in the corpus. Thus, it is ignored here 
# especially because its inclusion would cause serious problems with
# sentences like "Chodail sé tí Chóil".
# There are 7 entries for "tí" with different meanings in FGB.

# Avoid that "bac" is interpreted as a noun in cases like
# "nár bhac sé leis"
# Note: verb/noun pairs like "bac" are not rare in Irish. Thus,
# it makes perfect sense to put them into a list that might be
# extended as needed.
#LIST NOUN-VERB-PAIR = "bac";
REMOVE (n) IF (0 NOUN-VERB-PAIR) (-1 (vpart));

# -------------- @END ADDED CW 09/08 --------------

# ar uain, d´uain
#SELECT ("uain" n nom) IF (0 ("<uain>")) (-1 (pr));
# select uain/com unless it could possible be genitive of uan
#SELECT ("uain" n nom) IF (0 ("<uain>")) (-1 (det def)) (NOT -2 (n nom));

# -------
# uain vs uan (extended and amended 09/08 CW)
# -------

# "uain" is masculine:
# "na huain"
SELECT (n m pl nom) IF (0 ("uan" hPref)) (-1 ("<na>"));
# "méilean uain"
SELECT (n m sg gen) IF (0 ("<uain>")) (-1 ("méileach"));
# "uain ag méileach"
SELECT (n m) IF (0 ("uan")) (1 ("<ag>")) (2 ("méileach"));
# "A hUain Dé", "uain Cásca"
SELECT (n m) IF (0 ("uan")) (1 ("dia" n m sg gen) OR ("Cáisc" n f sg gen));
# "peata uain"
SELECT (n m sg gen) IF (0 ("<uain>")) (-1 ("peata"));
# "uain chaorach"
SELECT (n m) IF (0 ("uan")) (1 ("caora"));

# "uain" is feminine:
#LIST UAIN-FEM-SURR = ("an" det def) ("aon" det qnt) ("céad" det ord len);
# The simplest case "an uain"
SELECT (n f nom) IF (0 ("<uain>")) (-1 UAIN-FEM-SURR);
# Idiom: "ar uain nó ar éigin"
# Decided against verbatim pattern, i.e. ... (-1 ("<ar>")) (1 ("<nó>")) (3 ("<éigin>"))
# since there might be typos (missing fada).
# Also, ignored the second "ar", since it is always missinterpreted as "Cop Pres RelInd"
# If we check for "ar uain nó ... éigin" we can be quite sure that it is the idiom.
#SELECT (n f nom) IF (0 ("uain")) (-1 ("<ar>")) (1 (cnjcoo)) (3 (Adj Base));
SELECT (n f nom) IF (0 ("uain")) (-1 ("<ar>")) (1 (cnjcoo)) (3 ("<éigin>") OR ("<éigean>"));
SELECT (n m nom) IF (0 ("<éigin>")) (-1 ("<ar>")) ;
SELECT (n m nom) IF (0 ("<éigean>")) (-1 ("<ar>")) ;
# Common cases with preposition or adverb d'uain & cho(mh) uain
SELECT (n f nom) IF (0 ("uain")) (-1 (pr) OR (adv tts));
# ar a uain
SELECT (n f nom) IF (0 ("uain")) (-1 ("<a>" det pos)) (-2 (pr));
# "(a) uain a bheith ag duine" in the sense "(an) deis a bheith ag duine"
# is more common than "to have a lamb"
SELECT (n f nom) IF (0 ("uain")) (-1 ("bí")) (1 ("ag" pr));
# "tá bhur n-uain chun diúltú ag Orlaith..."
SELECT (n f nom) IF (0 ("uain")) (-2 ("bí")) (-1 (det pos));
# NOTE: It is hardly possible to disambiguate "tar" + "<uain>", e.g.
# "tháinig uain chuí" (occasion) vs "tháinig uain dhubha" (lambs), but if
# "<uain>" is preceded by a Det Poss, it is more likely than not the feminine
# noun, e.g. "tháinig m'uain chun faire"
SELECT (n f nom) IF (0 ("uain")) (-2 ("tar")) (-1 (det pos));
# "go bhfaighinn uain ar iad a léamh"
SELECT (n f nom) IF (0 ("uain")) (-1 ("faigh"));
# "Ní bhfuair mé uain a fháil amach"
SELECT (n f nom) IF (0 ("uain")) (-2 ("faigh")) (-1 (prn pers) OR (prn subj));
# select uain/com unless it could possible be genitive of uan
SELECT (n f nom) IF (0 ("uain")) (-1 (det def)) (NOT -2 (n nom));

# chuir sé a lámha timpeall/adv uirthi/prep-pron
REMOVE (n) IF (0 ("<timpeall>")) (1C (Pron Prep));
# Ní raibh aon bhac orthu gabháil/nv timpeall/adv
REMOVE (n) IF (0 ("<timpeall>")) (-1C (vblex subst));
#
# APR 2010
# bí cannot take a bare noun complement 
# e.g. Bhí sé dorcha!=Noun 
# e.g. Bhí Seán dorcha!=Noun 
# e.g. Bhí an oíche dorcha!=Noun 
#REMOVE (n) IF (*-1 ("bí") BARRIER (rel) OR (>>>) LINK *1 (n nom) OR (prn pers));
REMOVE (n) IF (-2 ("bí")) (-1 (prn pers));
REMOVE (n) IF (-2 ("bí")) (-1 (np));
REMOVE (n) IF (-3 ("bí")) (-2 (det def)) (-1 (n));
# -------------------------------------------------------------------- #
# S2 NUMERALS
# -------------------------------------------------------------------- #
# ****** IT MEANS ONE WHEN AMHÁIN FOLLOWS IT - FIND BARRIER SET ******
# aon can mean one/Num, any/Det or ace/Noun, but before another noun it is unlikely to mean ace/Noun
# e.g. Ní dhearna sé aon dochar ...
REMOVE (n) IF (0 ("aon")) ( 1 (n));

# 2 - 1  vs agus - agus
REMOVE (Num Op) IF (NOT -1 (Num Dig));

# here  aon = Det (any); not aon/Num (one) or aon/Noun (ace)
# e.g. ní raibh aon siopaí
REMOVE (Num Card) IF (0 ("aon")) (1C (n pl));

# cardinal num should be followed by a noun or "a chlog" only???
# e.g. "ní raibh sé mór" vs. "ní raibh sé ubh ann"
# le dhá chéad bhliain
# naoi gcéad seachtó's/Unknown a dó ...
REMOVE (Num Card) IF (NOT 1 (n) OR ("a" pr) OR (Num Card) OR (Unknown));

# -------------------------------------------------------------------- #
# S2 NOUNS 
# -------------------------------------------------------------------- #
# it is a gen noun if preceded by another noun/verbal noun/comp prep etc 

# cois chladaigh
SELECT (n gen) IF (-1 GEN-SIMP-PREP);
# cois na trá
SELECT (n gen) IF (-1 (det def)) (-2 GEN-SIMP-PREP);

# roinnt ama
SELECT (n gen) IF (-1 GEN-PART);
# roinnt bheag ama
SELECT (n gen) IF (-2 GEN-PART) (-1 (adj));

# tar éis scoile
SELECT (n gen) IF (-1 (pr));
# tar éis na scoile
SELECT (n gen) IF (-1 (det def)) (-2 (pr));

# cá bhfios don arm bocht ... dont want to include the Art in "don"
# BUT not "An fíor an ráiteas... "Léiríonn an pictiúir an méid ...
SELECT (n gen) IF (-1C (det def) ) (-2C (n)) (NOT -3 (det def));

# it is a noun (not verbal) if preceded by a numeral
# BUT exclude sé which is commonly a pronoun (he/it)
SELECT NOUN-NOT-VN IF (-1 (num)) (NOT -1 ("prpers" prn pers));
SELECT NOUN-NOT-VN IF (-1 (num)) (NOT -1 ("sé" prn pers));

# it is a noun if preceded by an unambiguous prep (C=careful mode)
SELECT (n) IF (-1C (pr));

# noun  should match following adj in gender, number and case
SELECT (n f) IF (0C (n)) (1 (adj f));
SELECT (n m) IF (0C (n)) (1 (adj m));
SELECT (n sg) IF (0C (n)) (1 (adj sg));
SELECT (n pl) IF (0C (n)) (1 (adj pl));
SELECT (n nom) IF (0C (n)) (1 (adj nom));
SELECT (n gen) IF (0C (n)) (1 (adj gen));
SELECT (n voc) IF (0C (n)) (1 (adj voc));

# noun  should match preceding article in gender, number and case
# an = Art Sg Def / Art Gen Sg Def Fem
# na = Art Pl Def
SELECT (n f sg gen) IF (-1C (det def f gen));
SELECT (n sg) IF (-1C (det def sg));
SELECT (n pl) IF (-1C (det def pl));

# TYPE 4 - after earlier disambiguation
# seomra suí
# BUT Chonaic fear bean (if bean could be gen)
# remove Com? exclude VN?  NO 
# le lucht imirce
SELECT (n gen) IF (-1C (n nom)) (-2C (pr));
# -------------------------------------------------------------------- #
# S2 DETERMINERS (demonstrative and poss.) 
# -------------------------------------------------------------------- #
# demonstrative determiners follow an NP not a VP
# e.g. Thaitin sin leis => pronoun not det.
REMOVE (det dem) IF (-1C (vblex));

# san = ins an(pr) vs. san = sin (det dem)
# sonraithe san ordachán, bagairt san Anschluss
REMOVE (det dem) IF (0 ("<san>")) (1C (n));

# do do leithéadh, do mo chrá: in these cases first "do" is Prep second "do" 
# is possessive determiner
# mar a d'éirigh leo where d' is vpart
REMOVE (det pos) IF (1 (det pos) OR (vpart));

# seo (this) or sin (that) can be either a pronoun or a demonstrative det.
# IT IS DEM IF FOLLOWS A *DEF* NOUN
# e.g. "rinne an madra seo (def)" but "rinne an madra é seo (prn) é"
# e.g. "í seo" This seo is most likely demonstrative rather than a pronoun >>NO
# NOTE the case for pron. will require transitivity info to resolve it
# rinne an madra (é) seo (the dog did this) = VP NP NP 
# an fear sin
SELECT (det dem) IF (-2 (det def)) (-1C (n));
# an dá fear sin
SELECT (det dem) IF (-3 (det def)) (-2 (num)) (-1C (n));
# an fear beag sin
# an deireadh seachtaine seo
# an liosta seiceála (VN-gen?) sin
SELECT (det dem) IF (-3 (det def)) (-2C (n)) (-1C (adj) OR (gen)) ;
# an dá fear beag sin
SELECT (det dem) IF (-4 (det def)) (-3 (num)) (-2C (n)) (-1C (adj) OR (gen)) ;

# corollary of above 
# e.g. Sin cuid den fhadhb ...
# Is iad/Pron sin/Det na sainceisteanna ..
REMOVE (det dem) IF (NOT -1 (n) OR (adj) );
# ====>>>> NOTE CHANGE -1 Pron ADDED from above rule

# iad siúd/Det
# díobh/leo siúd/PronPron Pers
#SELECT (det dem) IF (-1 (prn));
SELECT (det dem) IF (-1 (prn pers));

# is ionann sin ..
 REMOVE (det dem) IF (-2 (cop)) (-1 (adj) OR (prn));
# ====>>>> NOTE CHANGE -1 Pron REMOVED from above rule

#an saghas sin cainte
SELECT (det dem) IF (1C (n gen));
# -------------------------------------------------------------------- #
# S2 ARTICLE
# -------------------------------------------------------------------- #

# TYPE 1
# Art "na" is not the plural article unless the following noun is Pl
REMOVE (det def pl) IF (NOT 1 (n pl));

# TYPE 1
# e.g. an bhfuil - if bhfuil is a verb not a noun then an cannot be an art
REMOVE (det def) IF (1C (vblex));

# TYPE 1
# Art "na" is not a feminine article unless the following noun is Fem
REMOVE (det def f) IF ( NOT 1 (n f));

# NOTE
# an 19ú haois déag .. an t-aon ceann amháin, an droch rud ná, an CCEA 
REMOVE (det def) IF ( NOT 1 (n) OR (num) OR (det) OR (Abr) OR ADJ-PRENOM );

# it is an article if followed by definite noun 
# problem: ar an seilp -> Def forms should be "ar an seilpe" or "an tseilp"
# => remove DefArt on Noun from rule for robustness
# also remove Com on Noun  e.g. An fíor é? an=cop
SELECT (det def sg) IF (1C (n sg));
SELECT (det def pl) IF (1 (n pl));

# tidy up leftover DefArt after Prep Art etc. where there is an incorporated
# Art e.g. sa, don; on the basis that the last remining reading will not be removed; e.g. sa teach
REMOVE (defart) IF ( NOT -1 (det def));

# it is a fem gen article if followed by definite fem gen noun 
SELECT (det def f gen)  IF (1 (n f gen defart));

# an article can precede a numeral
# an chéad rud eile, an dara dul suas etc.
# an t-aon dráma
# ar an 1 Aibreán
SELECT (det def) IF (1 (num));

# "an" is is most likely an Art (rather than Cop) if  preceded by a prep (c) 
# and followed be a noun
# e.g. ar an gcuma san
# ar an gcéad dul síos
SELECT (det def) IF (-1C (pr)) (1 (n) OR NOUN-PREMOD);

# san = ins an (prep art)  / sin (det)
# san Earrach => san = prep art
# NO too general 
# e.g. ar an machnamh san dó/!=N
# an/Cop fear/N atá ann?
#SELECT (det def) IF (1C (n));

# scéalaíochta san na ridireachta
# san = "sin" not "ins an"
# FIXME
REMOVE (Prep Art) IF (1C (det def));
# den chéad uair
REMOVE (Prep Art) IF (NOT 1C (n) OR (Num Card));

# -------------------------------------------------------------------- #
# S2 PREPOSITION
# -------------------------------------------------------------------- #
# TYPE 1
# a is infinitival always before bheith/VNoun
# SELECT (pr) IF (1C ("<bheith>"));
SELECT (part inf) IF (1C ("<bheith>"));

# TYPE 1
# ar is prep always before ...
SELECT (pr) IF (1C ("<chlé>") OR ("<dheis>"));

# TYPE 1
#dhá dhath vs dhá choinneál dúinte: dhá usually means two and can only be a 
# prep possesssive before a (potential) verbal noun.
# REMOVE (Prep Poss) IF (0 ("<dhá>")) (NOT 1 (vblex subst))
REMOVE (pr) IF (0 ("<dhá>")) (1/1 (det pos)) (NOT 1 (vblex subst));

# TYPE 1
# e.g. i/prep do/poss theach, "do" is not a simp prep in this case ...
# e.g. do/prep do/poss  mháthair, 
# do/prep do/poss chrá, do/prep mo/poss chrá, dá/prep_poss crá
# but: thar a bheith, 
REMOVE (pr) IF (0 ("<do>")) (-1C (pr));

# TYPE 1
# it can't be a simp prep if not followed by an NP
REMOVE (pr) IF (NOT 1 POST-PREP);

# TYPE 2
# e.g. "ar nó roimh" 
# a chruthú trí/Prep agus/C ar/Prep ghréas 
# NOT le A nó le B; omit "a"
SELECT (pr) IF (NOT 0 ("a")) (1C (cnjcoo)) (2C (pr)) (NOT 2 ("a"));
SELECT (pr) IF (NOT 0 ("a")) (-1C (cnjcoo)) (-2C (pr)) (NOT -2 ("a"));

# le (simp prep) becomes leis before "an"
# otherwise leis is complex prep or occasionally a noun
# leis/PrepSimp an fhírinne a rá ...

# leis/PrepSimp sin, d'oscail an doras
REMOVE (pr) IF (0 ("<leis>")) (NOT 1 (det def) OR (dem));
# new
SELECT (pr) IF (0 ("<leis>")) (1C (det def) OR (dem));

# "a" functions as a prep in some phrases listed in A-PREP-PHR
REMOVE (pr) IF (0 ("a")) ( NOT 1 A-PREP-PHR OR (vblex subst len));
# not necessarily a prep if followed by any type of noun 
# e.g. "ar mhaith leat?" cop noun/adj prep-pron

# usually it is a (simple?) prep if followed by an article/det/num ??? or by a noun
# "de/PrepSimp chuid/Noun na/Art gCeilteach/Noun
# "as/PrepSimp Halltsatt/Noun
# BUT:  trí sheisiún could be prep or num
# "a" could be Det Poss or Prep Simp before Noun
# e.g. a shúil ar an pheata madaidh ...
SELECT (pr) IF (1C NOUN-PREMOD OR (n)) (NOT 0 ("<trí>") OR ("<a>"));

# NEW EUD
SELECT (part inf) IF (1C (vblex subst));

# TYPE 4 after diasamb of VN
# it is a (simple?) prep if followed by an verbal noun
# except if preceded by simp prep e.g. do do mholadh
# or i do chodladh
SELECT (pr) IF (1 (vblex subst)) (NOT -1 (pr));
# chun a fháil amach
SELECT (part inf) IF (1 (vblex subst)) (-1 ("<chun>"));

#############################
# Prep Rel no longer exist
# CHECK Part Rel
#SELECT (Prep Rel) IF (1C (vblex));
# "a (rel) mbíonn" vs "a (pos) hintinn"
#SELECT (Prep Poss) IF (1C (n)) (NOT -1 (pr));
SELECT (pr) IF (0/1 (det pos)) (1C (n)) (NOT -1 (pr));
# but note " le dhá(Num/PrepPoss) fhichid..."
# le(pr) dhá(num) bhliain
# BUT do(PrepSimp) mo(det pos) chrá
#REMOVE (Prep Poss) IF (-1 (pr));
REMOVE (pr) IF (-1 (pr)) (0/1 (det pos));

# TYPE 1/2
# in copular constructions like Is féidir X; X is frequently a Prep.
# e.g. is féidir linn: linn is Prep not Noun
SELECT (pr) IF (-1C ("féidir"));

# Thart/timpeall ar etc.
SELECT (pr) IF (1 (adv dir)) ;

# 4 SEP
# following doesnt work over long distances .. so limit it with punctuation
# also don't have more than two interrogatives ...
# e.g. Cad(=Q) chuige nach n-abrófá amhráin agus tú an fear ceol is fearr ar(!=Q) an mbaile?
SELECT (itg)    IF (1* ("?" sent) BARRIER (Punct Int))(NEGATE -1* (itg));
# -------------------------------------------------------------------- #
# S2 CONJUNCTIONS
# -------------------------------------------------------------------- #
# Agus is ceart do ...
REMOVE (cnjcoo) IF (0 ("agus")) (-1 ("agus")) ;
# tuairim is 300 bliain: is = conj in this context ...
SELECT (cnjcoo) IF (0 ("agus")) (-1 ("tuairim")) ;
# bán is buí
SELECT (cnjcoo) IF (0 ("agus")) (-1 (adj)) (1 (adj));
# Briain is Cormac
SELECT (cnjcoo) IF (0 ("agus")) (-1 (np)) (1 (np));

# in "mar a bhíodh" mar is conj rather than prep
SELECT (Conj) IF (1 (vpart)) (2 (vblex));
# nuair a d´ fhág
SELECT (Conj) IF (1 (vpart)) (2 (vpart)) (3 (vblex));
# nó "mar/nuair is eol duit", "mar/agus ba cheart"
SELECT (Conj) IF (1 (cop));

# Ó tháinig ann dó
SELECT (Conj) IF (0 ("ó")) (1C (vblex));

# e.g. óir beidh / óir ní bheidh
SELECT (Conj) IF (0 ("óir")) (1C (vblex));
SELECT (Conj) IF (0 ("óir")) (1 (vpart)) (2C (vblex));

# thall is abhus
SELECT (cnjcoo) IF (0 ("agus")) (1 ("abhus"));

# amach is amach
SELECT (cnjcoo) IF (0 ("agus")) (-1 ("amach")) (1 ("amach"));

# trí chéad is a trí, cúig céad is 50
# BUT is 50 duine a bhí ann; include -1 Num also
SELECT (cnjcoo) IF (0 ("agus")) (1 (Part Nm) OR (num)) (-1 (num));

# COMMA SEPARATED LISTS
SELECT (n) IF (1 (",")) (2 (n)) (3 (",")) (4 (n));
SELECT (n) IF (-1 (",")) (-2 (n)) (1 (",")) (2 (n));
SELECT (n) IF (-1 (",")) (-2 (n)) (-3 (",")) (-4 (n));

SELECT (vblex) IF (1 (",")) (2 (vblex)) (3 (",")) (4 (vblex));
SELECT (vblex) IF (-1 (",")) (-2 (vblex)) (1 (",")) (2 (vblex));
SELECT (vblex) IF (-1 (",")) (-2 (vblex)) (-3 (",")) (-4 (vblex));

SELECT (adj) IF (1 (",")) (2 (adj)) (3 (",")) (4 (adj));
SELECT (adj) IF (-1 (",")) (-2 (adj)) (1 (",")) (2 (adj));
SELECT (adj) IF (-1 (",")) (-2 (adj)) (-3 (",")) (-4 (adj));

# -------------------------------------------------------------------- #
# S2 DETERMINERS (possessive) - 
# -------------------------------------------------------------------- #
# a = "theirs" eclipses following noun, 
# a = "his" lenites following noun
# a = "hers" no initial mutation to following noun
# Poss includes Det Poss and Prep Poss
# e.g. lena = Le+Prep+Poss+3P etc...
# á úsáid ...
# Ecl and Len are not always present on Verbal Nouns - so omit ..
#REMOVE (pos pl) IF ( NOT 1 (n ecl) OR (vblex subst));
#REMOVE (pos m sg) IF ( NOT 1 (n len) OR (vblex subst));

## FIXME: Subreadings
#GÓD Added Pron Pers to identify Prep Poss before a Verbal Noun
#e.g. sé ina choladh, sí ina codladh, siad ina gcodladh
SELECT (det pos p3 m sg) IF (-1 (Pron Pers Masc) OR (n m)) (1 (vblex subst));
SELECT (det pos p3 f sg) IF (-1 (Pron Pers Fem) OR (n f)) (1 (vblex subst));
SELECT (det pos p3 pl) IF (-1 (Pron Pers Pl)) (1 (vblex subst));
#GÓD Match attributes of Verbal Noun with those of Prep Poss e.g. Len, bare or Ecl
#SELECT (vblex subst len) IF (-1 (Prep Poss Masc));
#REMOVE (vblex subst len) IF (-1 (Prep Poss Fem));
#REMOVE (vblex subst ecl) IF (-1 (Prep Poss Fem));
#SELECT (vblex subst ecl) IF (-1 (Prep Poss Pl));
SELECT (vblex subst len) IF (-1/-1 (pr)) (-1/-2 (det pos m));
REMOVE (vblex subst len) IF (-1/-1 (pr)) (-1/-2 (det pos f));
REMOVE (vblex subst ecl) IF (-1/-1 (pr)) (-1/-2 (det pos f));
SELECT (vblex subst ecl) IF (-1/-1 (pr)) (-1/-2 (det pos pl));

#SELECT (pos p3 pl) IF ( 1C (n ecl) OR (vblex subst));
SELECT (pos p3 m sg) IF ( 1C (n len) OR (vblex subst)) (NOT 1C (n voc));
# á hól
#SELECT (pos f sg) IF ( 1C (Noun hPref) OR (vblex subst));

REMOVE (det pos pl) IF ( NOT 1 (n ecl) );
REMOVE (det pos m sg) IF ( NOT 1 (n len));

SELECT (det pos pl) IF ( 1C (n ecl) ) (NOT 1 (vblex subst));
SELECT (det pos m sg) IF ( 1C (n len) ) (NOT 1C (n voc)) (NOT 1 (vblex subst));
# á hól
SELECT (det pos f sg) IF ( 1C (Noun hPref) ) (NOT 1 (vblex subst));
# a possessive determiner is not followed by a verb e.g. mo, do etc
# (can be followed by verbal noun - e.g. do mo chrá)
REMOVE (pos) IF (1C (vblex));

# select the posessive det reading (rather than noun) if followed by a noun
# e.g. rinneamar ár ndícheall 
# (if followed by gen. noun then could be the noun reading of ár)
SELECT (pos) IF (1C (n nom));
SELECT (pos) IF (0 ("<mo>") OR ("<do>")) (1 (vblex subst));

# -------------------------------------------------------------------- #
# S2 PRONOUNS 
# -------------------------------------------------------------------- #
# can't have a pronoun following an article
# e.g tar éis an dó
REMOVE (prn) IF (-1 (det def));
 
#GÓD
# cé is interog. pron rather than N or Conj
SELECT (prn itg) IF (0 ("cé")) (1 ("hé") OR ("hí") OR ("hiad") OR (det def));

# cé acu/aige/leis/air etc.
SELECT (prn itg) IF (0 ("cé")) (1 (Pron Prep));

# cé a rinne, a ndéanfadh, a mbíonn etc.
SELECT (prn itg) IF (1 ("a"));

# cé ba mhó
SELECT (prn itg) IF (1 (cop));

# TYPE 1
# Déan seo/sin/siúd
# san  (=sin) can be confused with the prep san so we exclude it
SELECT (Pron Dem) IF (-1C (vblex)) (NOT 0 ("<san>"));
# -------------------------------------------------------------------- #
# S2 INTERROGATIVES
# -------------------------------------------------------------------- #

# select interog reading if at start of sentence 
# and sentence ends in a ?
# includes Cop Q and Pron Q ...
#e.g. Cé/Q a bhí ann?
# Nach/NegQ raibh sé ann?
# NOTE: An/Q bhfuil tú a rá nach/Neg raibh sé ann? nach=Neg not NegQ
SELECT (itg) IF (-1 BOS) (*1 ("?" sent));
SELECT (itg neg) IF (-1 BOS) (*1 ("?" sent));

# new rule
REMOVE (itg)    IF  (NOT *1 ("?" sent));


# ar/Q chuala/V tú nach/Vb raibh/V sé ann?
# is/Cop maith an rud nár/Vb tháinig/V tú.
SELECT (Vb Neg Rel) IF (*-1C (vblex) OR (cop)) (1C (vblex)) ;

# 12 SEP new
# Cén duine nár fhoghlaim ar scoil ... ?
# Cén caisleán mór ar chas Bruce Springsteen ann ? 
SELECT (vblex) IF (-1 (rel)) (@1 (prn itg)) (NOT *-1 (vblex)) (NOT *1 (vblex)); 

# -------------------------------------------------------------------- #
# S2 COPULA
# -------------------------------------------------------------------- #
# COP INDEPENDANT = is, an, ar etc
# COP DEPENDANT = gur, nár, arbh etc

# Indep copula doesnt directly FOLLOW a verb
# e.g. cheannaigh sé ar/!=Cop an gcuma sin
# or verbal noun e.g. ag léamh an/!=Cop leabhair
# BUT dependant copular forms which introduce subordinate clauses
# e.g. creidim/V+S gur/Cop fear é
# or verb subject pron (sí, sé, siad) 
# e.g. má mheastar/Verb gur/Cop dóigh leis
# a cheapadh/VN gur/Cop chol ceathracha iad 
# e.g. má mheas/Verb sí gur/Cop dóigh leis
REMOVE (cop) IF (NOT 0 (Cop Dep)) (-1C (vblex) OR (subj) OR (vblex subst));

# copula doesnt PRECEDE a verb
# or verb subject pron (sí, sé, siad)
REMOVE (cop) IF (1C (vblex) OR (subj) OR (vblex subst));

# ==================================================================== #
# SECTION 3
#SECTION
CONSTRAINTS
# ==================================================================== #

# -------------------------------------------------------------------- #
# S3 VERBS
# -------------------------------------------------------------------- #
# if one of the possible readings is Verb and it is followed by a personal 
# pronoun e.g. mé, tú etc - then select this reading
# e.g. rinne mé
# Dá ndéanfaí é
# can have genuine nouns before pron e.g. chonaic sé ar scoil mé ...
# Note Ní cabhair é ...
SELECT (vblex) IF (1 (prn pers)) (NOT -1 (cop));
REMOVE (vblex) IF (1 (prn pers)) (-1C (cop));
# -------------------------------------------------------------------- #
# S3 VERBAL ADJS
# -------------------------------------------------------------------- #
#e.g. déanta de leathar, déanta domA
# but not "imithe chun a pósta/!=VA ar/Prep Thomás"
# "Faoi cheal an oiread sin d'fhios a labhartha/!=VA a/Prep bheith orra," 
SELECT (Verbal Adj) IF ( 1 (pr)) (NOT -1 (det pos));


SELECT (Verbal Adj) IF ( -1 (vblex subst)) ;

# -------------------------------------------------------------------- #
# S3 COPULA
# -------------------------------------------------------------------- #
#GÓD gach
# Níl bean ar liosta: ar = prep not cop
REMOVE (cop) IF (-2C (vblex)) (-1 (n) OR (subj))(NOT -1 ("gach"));

# Is maith liom 
# BUT not nach bhfuil le déanamh
SELECT (cop) IF ( 1C  (n) OR (adj) ) (2 ("le"));

#GÓD
# It is a copula if there is no verb to the left ...
# and if there is no verb to the right or fáth etc i.e. Cén fáth?
# TRY Prep Phrase ???
SELECT (cop) IF (NOT *-1 (vblex) ) (NOT *1 (vblex) OR ("<áit>") OR ("<chaoi>") OR ("<dóigh>") OR ("<fáth>") OR ("<tslí>") OR ("<mhéad>") Or ("<fhad>") OR ("<uair>"));

#if the sentence starts with any form of copula "is" choose this
# reading as long as it is not followed by a verb or fáth etc i.e. Cén fáth?
# Ní hé la na gaoth ... If it is at the start of a sentence choose Cop
SELECT (cop) IF (-1 BOS) (NOT 1 (vblex) OR ("<áit>") OR ("<chaoi>") OR ("<dóigh>") OR ("<fáth>") OR ("<tslí>") OR ("<mhéad>") Or ("<fhad>") OR ("<uair>"));

#It is a relative copula if preceded by the subject or direct object
# e.g.dir. rel: an áit is deise ar domhain , an bhean ab óige
# e.g. indir rel: fear nach cuimhin leis é, an duine ar leis an teach
# SHOULD DEFINE NP and use here ...
# IS THIS OK ????
SELECT (Cop Rel) IF (0C (cop)) (*-1 (n) OR (prn) OR (adj) );
REMOVE (Cop Rel) IF (NOT *-1 (n) OR (prn) OR (adj) );


# -------------------------------------------------------------------- #
# S3 PREPOSITION
# -------------------------------------------------------------------- #
# e.g. mar b'fhoireann - this mar is a Conj
# mar is gnáth
REMOVE (pr) IF (1C (cop));

# some forms are both prep simp and prep pron e.g. faoi, leis etc.
# "chuala sé faoi=prep-pron", but "chuala sé faoi=prep rud=noun"
# de/PrepSimp cheachtar/PronIdf
# this includes possibly "de céard", faoi seo,  etc ...
# liom/leat/leis/Pron Prep féin/PronRef
# ===>>> use 1C ???
#REMOVE (Pron Prep) IF (1 (n) OR (Pron Idf) OR (Pron Dem) OR (Pron itg));
REMOVE (pr) IF (0/1 (prn)) (1 (n) OR (prn ind) OR (prn dem) OR (prn itg));
# de 'thíreoalíocht'
#REMOVE (Pron Prep) IF (1 (Punct)) (2 (det def) OR (n) OR (Pron Idf) OR (Pron Dem) OR (prn itg));
REMOVE (pr) IF (0/1 (prn)) (1 (punct)) (2 (det def) OR (n) OR (prn ind) OR (prn dem) OR (prn itg));

# NEED LEXICAL RULES FOR SPECIFIC PREPS & VERBS
# not sure about including Prep ?????
# bhain sé de/PronPrep a chóta BUT  bhain sé geit as/PrepSimp a mhac
#SELECT (Pron Prep) IF (NOT 1 (det def) OR (n) OR (prn) OR (pr) OR (det pos));
#SELECT (Pron Prep) IF (1 (Punct)) (NOT 2 (n) OR (prn) OR (pr) OR (det pos));
SELECT (pr) IF (0/1 (det det)) (NOT 1 (det def) OR (n) OR (prn) OR (pr) OR (det pos));
SELECT (pr) IF (0/1 (det det)) (1 (punct)) (NOT 2 (n) OR (prn) OR (pr) OR (det pos));
# -------------------------------------------------------------------- #
# S3 CONJUNCTION
# -------------------------------------------------------------------- #

SELECT (cnjcoo) IF (1C (vblex) OR (vpart));

# e.g. mar atá, mar ba mhaith linn etc.
SELECT (cnjsub) IF (0 ("mar")) (1 (rel) OR (cop));

#Thuig siad go/Conj mbeadh/VerbCond
SELECT (cnjsub) IF (1 (vblex cni));

# e.g. cé chomh maith
# this comes after interogatives are tried "Cé chomh minic a rinne tú é?"
SELECT (cnjsub) IF (0 ("cé")) (1 ("chomh")) (NOT *1 ("?" sent));
# ==================================================================== #
# SECTION 4
#SECTION
CONSTRAINTS
# ==================================================================== #


# -------------------------------------------------------------------- #
# S4 INTERJECTIONS
# -------------------------------------------------------------------- #

# Lets say that interjections like Á must be at the start of a sentence
# or followed by punct e.g. Á, Ó! etc
REMOVE (ij) IF (NOT 1 (Punct)) ; 
SELECT (ij) IF (1 (Punct)) ; 

# -------------------------------------------------------------------- #
# S4 VERBAL NOUNS
# -------------------------------------------------------------------- #
# verbal noun or not?
# ... ach teacht i mo theannta.
# select the verbal-noun reading rather than noun reading for teacht 
# if there is no verb to the right


# -------------------------------------------------------------------- #
# S4 VERBS
# -------------------------------------------------------------------- #
# TYPE 4
# it is a form of verb if it is preceded by a verb part(C)
# d' éag ... -> éag= verb not noun
# a ghabhann ...
SELECT (vblex) IF (-1C (vpart));
#GÓD
SELECT (vblex) IF (-4 ("sin")); # Sin an fear a phléasc.
# -------------------------------------------------------------------- #
# S4 NOUNS
# -------------------------------------------------------------------- #

# TYPE 4
# e.g. dúil acu sa troid - select the noun reading for troid rather than verb reading
SELECT (n) IF (-1C (pr));
SELECT (NOUN-NOT-VN) IF (-1C (det def));

# TYPE 4
# nouns take com case after prep except for list of preps which take 
# gen case, (dat case is handled earlier)
SELECT (n nom) IF (-1C (pr)) (NOT -1 GEN-SIMP-PREP);

# TYPE 4
# arm na Róimhe => arm = Noun Com not Gen
SELECT (n nom) IF (1C (det def)) (2C (n gen));

# TYPE 4
# it is a definite noun if preceded by an article
# not including e.g. san (Prep Art), # don (Prep Art) 
# as the lenition here is because of the preposition e.g. don fhear(M), an fear
#GÓD e.g. san Afraic
SELECT (n defart) IF (-1C (det def));
# sa ghairdín, sa teach
# FIXME: subreadings
SELECT (n sg nom) IF (-1C (Prep Art Sg));
SELECT (n pl nom) IF (-1C (Prep Art Pl));
# a gcuid dúnta : dúnta is more likely to be Noun Gen than Adj
# after cuid; so select this reading
SELECT (n gen) IF (-1C ("cuid")) (-2 (det pos));

# Cé Árann 
SELECT (np) IF (1C (np)) (NOT 1 (Guess Prop));
# -------------------------------------------------------------------- #
# S4 ADJECTIVE
# -------------------------------------------------------------------- #
# e.g. cultúr na gCeilteach beo: gCeilteach is Gen Weak -> beo is Adj Weak
SELECT (Adj Weak) IF (-1C (Noun Weak)) ;
SELECT (Adj Strong) IF (-1C (Noun Strong)) ;

# e.g. ... a bheith cinnte
# BUT give preference to Verbal Adj
SELECT (Adj Base) IF (NOT 0 (Verbal Adj)) (-1C (vblex subst)) ;

# Ní mór .. nach maith , nach mór 
# Too many exceptions - leave this out
# e.g. nach liosta fada é seo ?
# SELECT (Adj Base) IF (-1C (cop)) ;
# ==================================================================== #
# SECTION 5
#SECTION
CONSTRAINTS
# ==================================================================== #

# -------------------------------------------------------------------- #
# S5 PREPOSITIONS
# -------------------------------------------------------------------- #
# e.g. mar sin/seo = Simp Prep + Pron Dem
# leis sin, as sin amach
SELECT (pr) IF (1 (Pron Dem));
SELECT (Pron Dem) IF (-1 (pr));

# -------------------------------------------------------------------- #
# S5 VERBS
# -------------------------------------------------------------------- #
# If there are a choice of inflected verb forms remaining - choose the autonomous one ....????
SELECT (vblex aut) IF (0C (vblex)) (NOT 1 (subj) OR (np));
# -------------------------------------------------------------------- #
# S5 NOUNS
# -------------------------------------------------------------------- #
# common noun is lenited only after 
# poss dets (mo/do/a theach)
# copula (ba dhuine mór é)
# numeral (dhá theach)
# prep simp (de chrann)
# prep poss (lena stór)
REMOVE (n nom len) IF (NOT -1 (Poss Sg) OR (cop) OR NUM-LEN OR (pr));
# FIXME: subreadings
#REMOVE (Noun Com Len) IF (NOT -1 (Poss Sg) OR (cop) OR NUM-LEN OR (Prep Simp) OR (Prep Art));
# ar an ngaoth
REMOVE (n nom ecl) IF (NOT -1 (Poss Pl) OR NUM-ECL OR ("i") OR ("<an>"));

# ar an ngalfchúrsa
# ar na céimeanna
REMOVE (n gen) IF (-1 ("an") OR ("na")) (-2 (pr));
#GÓD
# FIXME subreading
REMOVE (n gen) IF (-1C (Prep Art)); # sa ghairdín

SELECT (n nom len) IF (-1 (det pos m sg));
SELECT (n nom ecl) IF (-1 (det pos pl));


# "a deir" - direct relative verbal particle
SELECT (Direct) IF (1C ("abair"));

# ag aisteoireacht, ag léim etc where lenited and unlenited are same
REMOVE (Len) IF (0C (vblex subst)) (NOT -1 (pos) OR ("a"));
REMOVE (Ecl) IF (0C (vblex subst)) (NOT -1 (pos) OR ("a"));

# -------------------------------------------------------------------- #
# S5 ADJS
# -------------------------------------------------------------------- #
# when there is a choice following a Cop it is "usually" Adj
# Is deas/noun/adj/subst an lá
SELECT (adj) IF (-1C (cop));

# -------------------------------------------------------------------- #
# S5 TIDY UP
# -------------------------------------------------------------------- #
# Mí as a proper noun is always preceded by "na" 
# e.g. muintir na Mí, contae na Mí etc.
REMOVE (np) IF (0 ("<Mí>")) (NOT -1 ("<na>"));
SELECT (np) IF (0 ("<Mí>")) (-1 ("<na>"));
#GÓD
REMOVE (len) IF (-1C (np) OR (prn pers)); # Cheannaigh Seán leabhar agus léigh sé é.
# Ní náire feacadh i láthair Dé... remove the N and VN reading for ní when followed by another noun ... THIS IS NOT NECESSARILY ALWAYS CORRECT ...
REMOVE (n) IF (0 ("<Ní>")) (1C (n));

# disambiguate initial mutation ambiguity
SELECT (n len) IF  (-1 NOUN-LEN) (-1 (pr));
SELECT (n len) IF  (-1 NUM-LEN) ;
SELECT (n ecl) IF  (-1 NUM-ECL) ;
REMOVE (n len) IF  (NOT -1 NOUN-LEN) (-1 (pr));

#GÓD
# FIXME
SELECT ("do") IF (-1* ("tabhair") OR ("tabhairt")); # e.g. Thug mé cáca di.
SELECT ("de") IF (-1* ("bain") OR ("baint")); # e.g. Bhain sí a cóta di.
SELECT (Cop Past Dep) IF (1* (Verb PastInd) BARRIER (vblex)); # Dependent copula agrees with the verb for tense.
SELECT (Cop Pres Dep) IF (1* (Verb PresInd) BARRIER (vblex)); # Dependent copula agrees with the verb for tense.
SELECT (Noun Gen) IF (-1 (Det Poss))(-2 (Noun))(-3 (Prep Simp))(NOT -4 (vblex)); # le glór a fir.
REMOVE (Verb FutInd Len) IF (-1 (>>>)); # No lenition for sentence initial future tense. 
REMOVE (Verb FutInd Ecl) IF (-1 (>>>)); # No eclipsis for sentence initial future tense.
REMOVE (Num) IF (1 ("taobh"))(2 (Pron Prep)); # e.g. má tá sé taobh leis.
SELECT (vpart Rel Direct) IF (1 (Verb PastInd Auto)); # Account for non-lenition of Past Autonomous
SELECT ("teach") IF (0 ("<teach>") OR ("<Teach>"));
SELECT (Verbal Adj) IF (1 (Punct Fin))(NOT -2* (Noun)); # Bhí an geata dúnta.
SELECT (Prop Noun) IF (0 ("<Seán>")); # Seán is a proper noun
SELECT (Prep Simp) IF (-1 (Conj Coord))(-2 (Noun))(-3 (Prep Simp)); # Bhí sí ar buille agus ar mire.
SELECT (Coord) IF (-1 (Noun))(1 (Noun))(2 (Punct Fin)); # Is airde sliabh ná cnoc.
SELECT (Pl) IF (1 (Conj Coord))(2 (Noun Pl)); # Is fearr liom úlla ná oráistí.
REMOVE (Len) IF (-1 (Cop Pres)); # Is lá deas é.
REMOVE (Rel Indirect Pro) IF (1 (Dep Ecl))(2 (Det Poss)); # D'fhág an fear a bhfaca a mhac an bád.
REMOVE (Rel Indirect Pro) IF (1 (Verb VD));
REMOVE (vpart Rel Indirect Pro) IF (3* (Pron Prep)); # Sin an gort a gcuireadh an síol ann.
SELECT (Rel Indirect Pro) IF (-1 (Cop Pro Dem))(2 (Pron Prep)); # Sin a bhfuil ann.
SELECT (Rel Indirect Pro) IF (-1 (Prep))(Not 1 (Verb VD)); # Íocfaidh mé as a gceannóidh tú.
SELECT (RelInd Pro) IF (-1 ("gach")); # Rinne sé gach ar chóir a dhéanamh.
SELECT (Rel Indirect Pro) IF (0 (vpart Rel Indirect))(2 (Pron Prep)); # Is leat a bhfuil agam.
SELECT (vpart Rel Indirect Pro) IF (0 (vpart Rel Indirect))(1 (Verb VT Ecl))(NOT 2* (Noun) BARRIER (Prep)); # D'ól sé a bhfuair sé.
SELECT (vpart Rel Indirect Pro) IF (0 (vpart Rel Indirect))(1 (Verb Len))(NOT 2* (Noun) BARRIER (Prep)); # Sin ar chualamar.
REMOVE (Rel Indirect Pro) IF (NOT -1 (Prep))(NOT 2 (Pron Prep)); # Cén fáth a ndeachaigh sé amach?
REMOVE (Prep Simp) IF (-1 ("gach")); # Déan gach ar cheart a dhéanamh.
SELECT (Cop Past RelInd) IF (-1 ("gach"))(0 (Cop Past)); # Déan gach ar cheart a dhéanamh.
SELECT (Verb Imper) IF (1 ("gach"))(2 (vpart Rel Indirect)); # Díol gach a bhfuil agat.
REMOVE (Masc) IF (-2 ("gach"))(-1 ("<ar>")); # Rinne sé gach ar chóir a dhéanamh.
REMOVE (Subst) IF (-2 ("gach"))(-1 ("<ar>")); # Rinne sé gach ar chóir a dhéanamh.
SELECT (Coord) IF (NOT 1* (vblex)); # Thaistil Eoin ní ba mhó ná aon duine eile.
REMOVE (Adj) IF (0 ("leabhar"))(NOT -1 (Noun));
SELECT (Past) IF (1 (PastInd)); # Cár cheannaigh sé an leabhar?
REMOVE ("deifear") IF (0 ("deifir")); # Chuaigh sí amach faoi dheifir.
REMOVE (Cop Past) IF (-1C ("dá")); # Dá mba mise thú ní dhéanfainn é.
REMOVE (VTI) IF (2C (Art)); # Dheisigh Seán an rothar.
REMOVE (Len) IF (-1C ("go")); # Dúirt sé go rachadh sé.
SELECT (Pron Dem) IF (-2C ("chomh"))(-1C ("minic")); # Dúirt siad nach bhfeiceann siad an cineál seo chomh minic sin.
REMOVE (Len) IF (0 (Verbal Adj)); # léite as verbal adj is not lenited.
REMOVE (Len) IF (-1 (Cop Pro Dem)); # Sin rud nach dtuigim.
REMOVE (NegQ) IF (NOT 1* ("?" sent)); # Fear nach n-aontaíonn liom.
REMOVE (Cmpl) IF (NOT -1* (vblex)); # Fear nach n-aontaíonn liom.
REMOVE (Len) IF (-1 (Masc Com Sg)); # Dúirt sé rud éigin nár thuig mé.
SELECT (vblex subst) IF (NOT 1* (Part Inf)); # Mhol mé dó imeacht, rud nach ndearna sé.
SELECT ("de") IF (0 ("<d'>"))(1 ("airgead") OR ("aois")); # Chaill sé a raibh aige d'airgead.

REMOVE (cop) IF (1  ("<áit>") OR ("<chaoi>") OR ("<dóigh>") OR ("<fáth>") OR ("<tslí>") OR ("<mhéad>") Or ("<fhad>") OR ("<uair>"));
REMOVE (Len) IF (-2 (Punct Fin))(0 (Verb PresInd));
REMOVE (Ecl) IF (-2 (Punct Fin))(0 (Verb PresInd));
#REMOVE ("aimsear") IF (NOT 0 (VARIANT));
REMOVE ("tí") IF (0 (Noun Masc Gen Sg));
SELECT (Det Poss Sg) IF (2 (Pron Prep Sg));
REMOVE (Noun Ecl) IF (-1 (Det Poss Sg)); 
#SELECT (Prep Poss 3P Sg Fem) IF (1 ("deirfiúr"));
SELECT (1/-2 det pos p3 f sg) IF (1 ("deirfiúr"));
REMOVE (Noun Gen) IF (-1 ("beirt"));
REMOVE (cop) IF (1 (Punct));
#SELECT (Prep Poss Sg) IF (NOT 1 (Noun Ecl)); #  ní fhaigheann Máire freagra dá ceist.
#REMOVE (Prep Poss Masc) IF (NOT 1 (Noun Len)); #  ní fhaigheann Máire freagra dá ceist.
SELECT (Prep Poss Sg) IF (NOT 1 (Noun Ecl)); #  ní fhaigheann Máire freagra dá ceist.
REMOVE (Prep Poss Masc) IF (NOT 1 (Noun Len)); #  ní fhaigheann Máire freagra dá ceist.
SELECT ("do") IF (0 ("<dá>"))(1 ("ceist")); #  ní fhaigheann Máire freagra dá ceist.
SELECT (Cop Cond) IF (1 ("féidir")); # Arbh fhéidir gurb é Séamus é?
SELECT (Adv) IF (-1* ("féach") BARRIER (Punct));
SELECT (Conj Subord) IF (1 ("ar"))(2 ("dóigh"));
REMOVE (Noun Masc) IF (NOT 0 ("<mianach>")); # mian Masc should be related to mianach.
SELECT (Noun) IF (-1* ("clois") BARRIER (Punct)); # Cloiseann sí fuaim,
REMOVE (Det Poss 3P Sg Masc) IF (-2* (n f sg nom) BARRIER (vblex));
REMOVE (Noun Len) IF (-1C (Det Poss 3P Sg)); # Tá Máire ina suí agus tá a lámha os comhair a súl aici.
REMOVE (Noun Voc) IF (NOT -1C (Part Voc));
#=====#
END #
#=====#



